
Процедура СформироватьТекстЗапроса() Экспорт
	
	МассивИменМетаданных = Новый Массив();
	Если ЗначениеЗаполнено(ИмяРегистра) Тогда
		МассивИменМетаданных.Добавить(ИмяРегистра);
	Иначе
		
		Для каждого Регистр из Метаданные.РегистрыНакопления цикл
			Если Регистр.ВидРегистра <> Метаданные.СвойстваОбъектов.ВидРегистраНакопления.Остатки тогда
				продолжить;
			КонецЕсли;
			МассивИменМетаданных.Добавить("РегистрНакопления."+Регистр.Имя);
		КонецЦикла;
		
		Для каждого Регистр из Метаданные.РегистрыБухгалтерии цикл
			МассивИменМетаданных.Добавить("РегистрБухгалтерии."+Регистр.Имя);
		КонецЦикла;
	
	КонецЕсли;	
	
	СтруктБД=ПолучитьСтруктуруХраненияБазыДанных(МассивИменМетаданных,Истина);
	
	БазовыйЗапроскТаблице = "select '%%ИмяРегистра%%' as [Имя регистра],
	|_period as [Дата итога],
	|COUNT(*) AS [Число строк всего],
	|SUM(Case When %%ОтборНулевыхСтрокРесурсов%% then 1 else 0 end) as [Нулевые строки]
	|from %%ТекущаяТаблица%%
	|group by _period";
	СписокРесурсов = Новый Массив;	
	ТекстЗапросакSQL = "";
	ПерваяТаблица = Истина;
	НайденныеТаблицыИтогов = Новый Массив;
	Для каждого таблица из СтруктБД цикл
		Если Найти(таблица.Назначение,"Итоги")>0 тогда
			НайденныеТаблицыИтогов.Добавить(таблица);
		КонецЕсли;
	КонецЦикла;
	//НайденныеТаблицыИтогов = СтруктБД.НайтиСтроки(Новый Структура("Назначение","Итоги"));
	Для каждого ТаблицаИтоговSQL из НайденныеТаблицыИтогов цикл
		СтрокаОтбораПоРесурсам="";
		
		СписокРесурсов.Очистить();
		Для каждого Поля из ТаблицаИтоговSQL.Поля цикл
			Если Найти(Поля.Метаданные,".Ресурс")>0 тогда
				СписокРесурсов.Добавить(Поля);
			КонецЕсли;
		КонецЦикла;
		
		Если СписокРесурсов.Количество()=0 тогда
			продолжить;
		КонецЕсли;
		
		ПервыйРесурс = Истина;
		Для каждого Ресурс из СписокРесурсов цикл
			СтрокаОтбораПоРесурсам = СтрокаОтбораПоРесурсам+?(ПервыйРесурс,""," and ") + Ресурс.ИмяПоляХранения+" = 0 ";
			ПервыйРесурс=Ложь;
		КонецЦикла;
		
		ТексЗапросДляПодстановки = СтрЗаменить(БазовыйЗапроскТаблице,"%%ИмяРегистра%%",ТаблицаИтоговSQL.Метаданные+ТаблицаИтоговSQL.Назначение);
		ТексЗапросДляПодстановки = СтрЗаменить(ТексЗапросДляПодстановки,"%%ОтборНулевыхСтрокРесурсов%%",СтрокаОтбораПоРесурсам);
		ТексЗапросДляПодстановки = СтрЗаменить(ТексЗапросДляПодстановки,"%%ТекущаяТаблица%%",ТаблицаИтоговSQL.ИмяТаблицыХранения);
		
		ТекстЗапросакSQL = ТекстЗапросакSQL  +
		?(ПерваяТаблица,""," 
		| UNION
		|")		+ ТексЗапросДляПодстановки;	
		ПерваяТаблица=Ложь;
		
	КонецЦикла;
	Если ТекстЗапросакSQL<>"" тогда	
		ТекстЗапросакSQL = ТекстЗапросакSQL+"
		|order by _period desc,[Нулевые строки] desc";
	КонецЕсли;
	
	Текст = ТекстЗапросакSQL;
	
КонецПроцедуры
