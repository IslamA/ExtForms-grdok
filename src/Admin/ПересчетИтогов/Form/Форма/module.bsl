
Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	ИспользоватьМонопольныйРежим = Истина;
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	ЗаполнитьДерево();
	
КонецПроцедуры

Процедура УстановитьПометкуГруппы(Стр)
	Если Стр.Строки.Итог("Пометка") = 0 Тогда
		Стр.Пометка = 0;
	ИначеЕсли Стр.Строки.Итог("Пометка") = Стр.Строки.Количество() Тогда
		Стр.Пометка = 1;
	Иначе
		Стр.Пометка = 2;
	КонецЕсли;	
КонецПроцедуры	

Процедура ЗаполнитьДеревоПоМетаданным(Стр)
	
	Для Каждого мОбъект ИЗ Метаданные[Стр.Имя] Цикл
		
		НоваяСтр     = Стр.Строки.Добавить();
		НоваяСтр.Имя = мОбъект.Имя;
		НоваяСтр.Представление = мОбъект.Представление();
		
		Если ВыбранныеМетаданные.НайтиПоЗначению(НоваяСтр.Имя) <> Неопределено Тогда
			НоваяСтр.Пометка = 1;
		КонецЕсли;
		
		НоваяСтр.Картинка = Стр.Картинка;
		
		Если Стр.Имя = "РегистрыНакопления" Тогда
			
			Итоги            = РегистрыНакопления[НоваяСтр.Имя].ПолучитьИспользованиеИтогов();
			
			Если мОбъект.РазрешитьРазделениеИтогов Тогда
				РазделениеИтогов = РегистрыНакопления[НоваяСтр.Имя].ПолучитьРежимРазделенияИтогов();
			Иначе		
				РазделениеИтогов = "";
			КонецЕсли;	
			
		ИначеЕсли Стр.Имя = "РегистрыБухгалтерии" Тогда
			
			Итоги            = РегистрыБухгалтерии[НоваяСтр.Имя].ПолучитьИспользованиеИтогов();
			
			Если мОбъект.РазрешитьРазделениеИтогов Тогда
				РазделениеИтогов = РегистрыБухгалтерии[НоваяСтр.Имя].ПолучитьРежимРазделенияИтогов();
			Иначе		
				РазделениеИтогов = "";
			КонецЕсли;	
				
		КонецЕсли;	
			
		НоваяСтр.Итоги            = Итоги;
		НоваяСтр.РазделениеИтогов = РазделениеИтогов;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьДерево()
	
	Стр = Дерево.Строки.Добавить();
	Стр.Имя           = "РегистрыНакопления";
	Стр.Представление = "Регистры накопления";
	Стр.Картинка      = "РегистрНакопления";
	
	ЗаполнитьДеревоПоМетаданным(Стр);
	УстановитьПометкуГруппы(Стр);
	
	Стр = Дерево.Строки.Добавить();
	Стр.Имя           = "РегистрыБухгалтерии";
	Стр.Представление = "Регистры бухгалтерии";
	Стр.Картинка      = "РегистрБухгалтерии";
	
	ЗаполнитьДеревоПоМетаданным(Стр);
	УстановитьПометкуГруппы(Стр);
	
КонецПроцедуры

Процедура ДеревоРегистровПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	ОформлениеСтроки.Ячейки.Представление.УстановитьФлажок(ДанныеСтроки.Пометка);
	ОформлениеСтроки.Ячейки.Представление.УстановитьКартинку(БиблиотекаКартинок[ДанныеСтроки.Картинка]);
	
	Если ДанныеСтроки.Родитель <> Неопределено Тогда
		
		Если ДанныеСтроки.Итоги Тогда
			ОформлениеСтроки.ЦветТекста = WebЦвета.Черный;
		Иначе	
			ОформлениеСтроки.ЦветТекста = WebЦвета.Серый;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДеревоРегистровПриИзмененииФлажка(Элемент, Колонка)
	
	ТекСтрока = Элемент.ТекущаяСтрока;
	Если ТекСтрока.Пометка = 2 Тогда
		Текстрока.Пометка = 1;
	Иначе	
		ТекСтрока.Пометка = 1 - ТекСтрока.Пометка;
	КонецЕсли;
	
	Если ТекСтрока.Строки.Количество() > 0 Тогда
		Для Каждого Стр ИЗ ТекСтрока.Строки Цикл
			Стр.Пометка = ТекСтрока.Пометка;
		КонецЦикла;
	КонецЕсли;
	
	Если ТекСтрока.Родитель <> Неопределено Тогда
		УстановитьПометкуГруппы(ТекСтрока.Родитель);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДействияФормыВыполнить(Кнопка)
	
	УстановитьМонопольныйРежим(Истина);
	
	Для каждого Строка1 Из Дерево.Строки Цикл
		
		Для каждого Строка2 Из Строка1.Строки Цикл
			
			Если Строка2.Пометка <> 1 Тогда
				Продолжить;
			КонецЕсли;
			
			Сообщить(Строка1.Представление + ": " + Строка2.Представление);
			Сообщить("Начало: " + ТекущаяДата());
			
			Если Строка1.Имя = "РегистрыНакопления" Тогда
				МенеджерОбъекта = РегистрыНакопления[Строка2.Имя];
			Иначе	
				МенеджерОбъекта = РегистрыБухгалтерии[Строка2.Имя];
			КонецЕсли;	
			
			Если ЗаПериод Тогда
				МенеджерОбъекта.ПересчитатьИтогиЗаПериод(НачПериода, КонПериода);
			Иначе
				
				Если Строка1.Имя = "РегистрыНакопления" Тогда
					МенеджерОбъекта.ПересчитатьИтоги();
					Если Метаданные.РегистрыНакопления[Строка2.Имя].ВидРегистра = Метаданные.СвойстваОбъектов.ВидРегистраНакопления.Остатки Тогда
						МенеджерОбъекта.ПересчитатьТекущиеИтоги();
					КонецЕсли;
				Иначе
					МенеджерОбъекта.ПересчитатьИтоги();
					МенеджерОбъекта.ПересчитатьТекущиеИтоги();
				КонецЕсли;
			КонецЕсли;
			Сообщить("Окончание: " + ТекущаяДата());
			
		КонецЦикла;
		
	КонецЦикла;
	
	УстановитьМонопольныйРежим(Ложь);
	
КонецПроцедуры

Процедура ВыбПериодНажатие(Элемент)
	НастройкаПериода = Новый НастройкаПериода;
	НастройкаПериода.УстановитьПериод(НачПериода, ?(КонПериода='0001-01-01', КонПериода, КонецДня(КонПериода)));
	НастройкаПериода.РедактироватьКакИнтервал = Истина;
	НастройкаПериода.РедактироватьКакПериод = Истина;
	НастройкаПериода.ВариантНастройки = ВариантНастройкиПериода.Период;
	Если НастройкаПериода.Редактировать() Тогда
		НачПериода = НастройкаПериода.ПолучитьДатуНачала();
		КонПериода = НастройкаПериода.ПолучитьДатуОкончания();
	КонецЕсли;
КонецПроцедуры

Процедура ДействияФормыОтключитьИтоги(Кнопка)
	
	УстановитьМонопольныйРежим(Истина);
	
	Для каждого Строка1 Из Дерево.Строки Цикл
		
		Для каждого Строка2 Из Строка1.Строки Цикл
			
			Если Строка2.Пометка <> 1 Тогда
				Продолжить;
			КонецЕсли;
			
			Сообщить(Строка1.Представление + ": " + Строка2.Представление);
			Сообщить("Начало: " + ТекущаяДата());
			
			Если Строка1.Имя = "РегистрыНакопления" Тогда
				МенеджерОбъекта = РегистрыНакопления[Строка2.Имя];
			Иначе	
				МенеджерОбъекта = РегистрыБухгалтерии[Строка2.Имя];
			КонецЕсли;	
			
			Если ЗаПериод Тогда
				МенеджерОбъекта.ПересчитатьИтогиЗаПериод(НачПериода, КонПериода);
			Иначе
				
				Если Строка1.Имя = "РегистрыНакопления" Тогда
					МенеджерОбъекта.УстановитьИспользованиеИтогов(Ложь);
					Если Метаданные.РегистрыНакопления[Строка2.Имя].ВидРегистра = Метаданные.СвойстваОбъектов.ВидРегистраНакопления.Остатки Тогда
						МенеджерОбъекта.УстановитьИспользованиеТекущихИтогов(Ложь);
					КонецЕсли;
				Иначе
					МенеджерОбъекта.УстановитьИспользованиеИтогов(Ложь);
					МенеджерОбъекта.УстановитьИспользованиеТекущихИтогов(Ложь);
				КонецЕсли;
			КонецЕсли;
			Сообщить("Окончание: " + ТекущаяДата());
			
			Строка2.Итоги = Ложь;
			
		КонецЦикла;
		
	КонецЦикла;
	
	УстановитьМонопольныйРежим(Ложь);
	
КонецПроцедуры

Процедура ДействияФормыВключитьИтоги(Кнопка)
	
	Если ИспользоватьМонопольныйРежим Тогда
		УстановитьМонопольныйРежим(Истина);
	КонецЕсли;	
	
	Для каждого Строка1 Из Дерево.Строки Цикл
		
		Для каждого Строка2 Из Строка1.Строки Цикл
			
			Если Строка2.Пометка <> 1 Тогда
				Продолжить;
			КонецЕсли;
			
			Сообщить(Строка1.Представление + ": " + Строка2.Представление);
			Сообщить("Начало: " + ТекущаяДата());
			
			Если Строка1.Имя = "РегистрыНакопления" Тогда
				МенеджерОбъекта = РегистрыНакопления[Строка2.Имя];
			Иначе	
				МенеджерОбъекта = РегистрыБухгалтерии[Строка2.Имя];
			КонецЕсли;	
			
			Если ЗаПериод Тогда
				МенеджерОбъекта.ПересчитатьИтогиЗаПериод(НачПериода, КонПериода);
			Иначе
				
				Если Строка1.Имя = "РегистрыНакопления" Тогда
					МенеджерОбъекта.УстановитьИспользованиеИтогов(Истина);
					Если Метаданные.РегистрыНакопления[Строка2.Имя].ВидРегистра = Метаданные.СвойстваОбъектов.ВидРегистраНакопления.Остатки Тогда
						МенеджерОбъекта.УстановитьИспользованиеТекущихИтогов(Истина);
					КонецЕсли;
				Иначе
					МенеджерОбъекта.УстановитьИспользованиеИтогов(Истина);
					МенеджерОбъекта.УстановитьИспользованиеТекущихИтогов(Истина);
				КонецЕсли;
			КонецЕсли;
			Сообщить("Окончание: " + ТекущаяДата());
			
			Строка2.Итоги = Истина;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Если ИспользоватьМонопольныйРежим Тогда
		УстановитьМонопольныйРежим(Ложь);
	КонецЕсли;	
	
КонецПроцедуры
