НаСервере
Процедура ЗаполнитьДерево()
	
	Стр = Дерево.ПолучитьЭлементы().Добавить();
	Стр.Имя           = "РегистрыНакопления";
	Стр.Представление = "Регистры накопления";
	Стр.Картинка      = "РегистрНакопления";
	
	ЗаполнитьДеревоПоМетаданным(Стр);
	УстановитьПометкуГруппы(Дерево, Стр);
	
	Стр = Дерево.ПолучитьЭлементы().Добавить();
	Стр.Имя           = "РегистрыБухгалтерии";
	Стр.Представление = "Регистры бухгалтерии";
	Стр.Картинка      = "РегистрБухгалтерии";
	
	ЗаполнитьДеревоПоМетаданным(Стр);
	УстановитьПометкуГруппы(Дерево, Стр);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПометкуГруппы(Дерево, Стр)
	
	ИтогоПометка = 0;
	Колво        = 0;
	
	Строки = Стр.ПолучитьЭлементы();
	Для каждого Строка из Строки Цикл
		
		Если Строка.Пометка = 1 Тогда
			ИтогоПометка = ИтогоПометка + 1; 
		КонецЕсли;	
		
		Колво = Колво + 1;
		
	КонецЦикла;	
	
	Если ИтогоПометка = 0 Тогда
		Стр.Пометка = 0;
	ИначеЕсли ИтогоПометка = Колво Тогда
		Стр.Пометка = 1;
	Иначе
		Стр.Пометка = 2;
	КонецЕсли;	
	
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьДеревоПоМетаданным(Стр)
	
	Для Каждого мОбъект ИЗ Метаданные[Стр.Имя] Цикл
		
		НоваяСтр     = Стр.ПолучитьЭлементы().Добавить();
		НоваяСтр.Имя = мОбъект.Имя;
		НоваяСтр.Представление = мОбъект.Представление();
		
		Если Объект.ВыбранныеМетаданные.НайтиПоЗначению(НоваяСтр.Имя) <> Неопределено Тогда
			НоваяСтр.Пометка = 1;
		КонецЕсли;
		
		НоваяСтр.Картинка = Стр.Картинка;
		
		Если Стр.Имя = "РегистрыНакопления" Тогда
			
			Итоги            = РегистрыНакопления[НоваяСтр.Имя].ПолучитьИспользованиеИтогов();
			
			Если мОбъект.РазрешитьРазделениеИтогов Тогда
				РазделениеИтогов = РегистрыНакопления[НоваяСтр.Имя].ПолучитьРежимРазделенияИтогов();
			Иначе		
				РазделениеИтогов = "";
			КонецЕсли;	
			
		ИначеЕсли Стр.Имя = "РегистрыБухгалтерии" Тогда
			
			Итоги            = РегистрыБухгалтерии[НоваяСтр.Имя].ПолучитьИспользованиеИтогов();
			
			Если мОбъект.РазрешитьРазделениеИтогов Тогда
				РазделениеИтогов = РегистрыБухгалтерии[НоваяСтр.Имя].ПолучитьРежимРазделенияИтогов();
			Иначе		
				РазделениеИтогов = "";
			КонецЕсли;	
				
		КонецЕсли;	
			
		НоваяСтр.Итоги            = Итоги;
		НоваяСтр.РазделениеИтогов = РазделениеИтогов;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПометкаПриИзменении(Элемент)
	
	ТекСтрока = Элементы.Дерево.ТекущиеДанные;
	Если ТекСтрока.Пометка = 2 Тогда
		Текстрока.Пометка = 0;
	КонецЕсли;
	
	Строки = ТекСтрока.ПолучитьЭлементы();
	Если Строки.Количество() > 0 Тогда
		Для Каждого Стр ИЗ Строки Цикл
			Стр.Пометка = ТекСтрока.Пометка;
		КонецЦикла;
	КонецЕсли;
	
	Родитель = ТекСтрока.ПолучитьРодителя();
	Если Родитель <> Неопределено Тогда
		УстановитьПометкуГруппы(Дерево, Родитель);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчетИтогов(Команда)
	
	Для каждого Строка1 Из Дерево.ПолучитьЭлементы() Цикл
		
		Для каждого Строка2 Из Строка1.ПолучитьЭлементы() Цикл
			
			Если Строка2.Пометка <> 1 Тогда
				Продолжить;
			КонецЕсли;
			
			Сообщить(Строка1.Представление + ": " + Строка2.Представление);
			Сообщить("Начало: " + ТекущаяДата());
			
			ПересчетИтоговНаСервере(Строка1.Имя, Строка2.Имя);
			Сообщить("Окончание: " + ТекущаяДата());
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПересчетИтоговНаСервере(Тип, Имя)
	
	Если Тип = "РегистрыНакопления" Тогда
		МенеджерОбъекта = РегистрыНакопления[Имя];
	Иначе	
		МенеджерОбъекта = РегистрыБухгалтерии[Имя];
	КонецЕсли;	
	
	Если Объект.ЗаПериод Тогда
		МенеджерОбъекта.ПересчитатьИтогиЗаПериод(Объект.НачПериода, Объект.КонПериода);
	Иначе
		
		Если Тип = "РегистрыНакопления" Тогда
			МенеджерОбъекта.ПересчитатьИтоги();
			Если Метаданные.РегистрыНакопления[Имя].ВидРегистра = Метаданные.СвойстваОбъектов.ВидРегистраНакопления.Остатки Тогда
				МенеджерОбъекта.ПересчитатьТекущиеИтоги();
			КонецЕсли;
		Иначе
			МенеджерОбъекта.ПересчитатьИтоги();
			МенеджерОбъекта.ПересчитатьТекущиеИтоги();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


