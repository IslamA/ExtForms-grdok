
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Строки = ДеревоМетаданных.ПолучитьЭлементы();
	
	НоваяСтрока = Строки.Добавить();
	НоваяСтрока.Имя = "Справочники";
	НоваяСтрока.ПолноеИмя = "Справочники";
	НоваяСтрока.ИндексКартинки = 0;
	НоваяСтрока.ПолучитьЭлементы().Добавить();
	
	НоваяСтрока = Строки.Добавить();
	НоваяСтрока.Имя = "Документы";
	НоваяСтрока.ПолноеИмя = "Документы";
	НоваяСтрока.ИндексКартинки = 1;
	НоваяСтрока.ПолучитьЭлементы().Добавить();
	
	НоваяСтрока = Строки.Добавить();
	НоваяСтрока.Имя = "Отчеты";
	НоваяСтрока.ПолноеИмя = "Отчеты";
	НоваяСтрока.ИндексКартинки = 6;
	НоваяСтрока.ПолучитьЭлементы().Добавить();
	
	НоваяСтрока = Строки.Добавить();
	НоваяСтрока.Имя = "Обработки";
	НоваяСтрока.ПолноеИмя = "Обработки";
	НоваяСтрока.ИндексКартинки = 5;
	НоваяСтрока.ПолучитьЭлементы().Добавить();
	
	НоваяСтрока = Строки.Добавить();
	НоваяСтрока.Имя = "ПланыВидовХарактеристик";
	НоваяСтрока.ПолноеИмя = "ПланыВидовХарактеристик";
	НоваяСтрока.ИндексКартинки = 11;
	НоваяСтрока.ПолучитьЭлементы().Добавить();
	
	НоваяСтрока = Строки.Добавить();
	НоваяСтрока.Имя = "ПланыСчетов";
	НоваяСтрока.ПолноеИмя = "ПланыСчетов";
	НоваяСтрока.ИндексКартинки = 12;
	НоваяСтрока.ПолучитьЭлементы().Добавить();
	
	НоваяСтрока = Строки.Добавить();
	НоваяСтрока.Имя = "ПланыВидовРасчета";
	НоваяСтрока.ПолноеИмя = "ПланыВидовРасчета";
	НоваяСтрока.ИндексКартинки = 13;
	НоваяСтрока.ПолучитьЭлементы().Добавить();
	
	НоваяСтрока = Строки.Добавить();
	НоваяСтрока.Имя = "РегистрыСведений";
	НоваяСтрока.ПолноеИмя = "РегистрыСведений";
	НоваяСтрока.ИндексКартинки = 2;
	НоваяСтрока.ПолучитьЭлементы().Добавить();
	
	НоваяСтрока = Строки.Добавить();
	НоваяСтрока.Имя = "РегистрыНакопления";
	НоваяСтрока.ПолноеИмя = "РегистрыНакопления";
	НоваяСтрока.ИндексКартинки = 3;
	НоваяСтрока.ПолучитьЭлементы().Добавить();
	
	НоваяСтрока = Строки.Добавить();
	НоваяСтрока.Имя = "РегистрыБухгалтерии";
	НоваяСтрока.ПолноеИмя = "РегистрыБухгалтерии";
	НоваяСтрока.ИндексКартинки = 14;
	НоваяСтрока.ПолучитьЭлементы().Добавить();
	
	НоваяСтрока = Строки.Добавить();
	НоваяСтрока.Имя = "РегистрыРасчета";
	НоваяСтрока.ПолноеИмя = "РегистрыРасчета";
	НоваяСтрока.ИндексКартинки = 15;
	НоваяСтрока.ПолучитьЭлементы().Добавить();
	
	НоваяСтрока = Строки.Добавить();
	НоваяСтрока.Имя = "БизнесПроцессы";
	НоваяСтрока.ПолноеИмя = "БизнесПроцессы";
	НоваяСтрока.ИндексКартинки = 4;
	НоваяСтрока.ПолучитьЭлементы().Добавить();
	
	НоваяСтрока = Строки.Добавить();
	НоваяСтрока.Имя = "Задачи";
	НоваяСтрока.ПолноеИмя = "Задачи";
	НоваяСтрока.ИндексКартинки = 10;
	НоваяСтрока.ПолучитьЭлементы().Добавить();
	
КонецПроцедуры

&НаКлиенте
Процедура НайтиОпции(Команда)
	НайтиОпцииНаСервере();
КонецПроцедуры

&НаСервере
Процедура НайтиОпцииНаСервере()
	
	ТекущаяСтрока = Элементы.ДеревоМетаданных.ТекущаяСтрока;
	ТекущиеДанные = ДеревоМетаданных.НайтиПоИдентификатору(ТекущаяСтрока);
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	ПолноеИмя = СтрЗаменить(ТекущиеДанные.ПолноеИмя, ".", Символы.ПС);
	ИскомыйОбъект = ПолучитьОбъектМетаданных(ПолноеИмя);
	
	Опции.Очистить();
	
	Для каждого ОбъектМетаданных из Метаданные.ФункциональныеОпции Цикл
		
		Если Ложь Тогда
			ОбъектМетаданных = Метаданные.ФункциональныеОпции.БазоваяВерсия;
		КонецЕсли;
		
		Для каждого Элемент Из ОбъектМетаданных.Состав Цикл
			
			Если Элемент.Объект = ИскомыйОбъект Тогда
				
				НоваяСтрока = Опции.Добавить();
				НоваяСтрока.Имя      = ОбъектМетаданных.Имя;
				НоваяСтрока.Хранение = ОбъектМетаданных.Хранение.ПолноеИмя();
				НоваяСтрока.Значение = ПолучитьФункциональнуюОпцию(ОбъектМетаданных.Имя);
				
			КонецЕсли;	
			
		КонецЦикла;	
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоМетаданныхПередРазворачиванием(Элемент, Строка, Отказ)
	
	ДеревоМетаданныхПередРазворачиваниемНаСервере(Строка, Отказ);
	
КонецПроцедуры

&НаСервере
Процедура ДеревоМетаданныхПередРазворачиваниемНаСервере(Строка, Отказ)
	
	ТекущаяСтрока = ДеревоМетаданных.НайтиПоИдентификатору(Строка);
	Если ТекущаяСтрока.УзелРазвернут Тогда
		Возврат;
	КонецЕсли;	
	
	Строки = ТекущаяСтрока.ПолучитьЭлементы();
	Строки.Очистить();
	
	Попытка
		
		Для каждого ОбъектМетаданных из ПолучитьКоллекциюОбъектаМетаданных(ТекущаяСтрока.ПолноеИмя) Цикл
			
			Если ТипЗнч(ОбъектМетаданных) = Тип("Структура") Тогда
				Представление = ОбъектМетаданных.Представление;
				ИндексКартинки = -1;
			Иначе	
				Представление = ОбъектМетаданных.Представление();
				ИндексКартинки = ТекущаяСтрока.ИндексКартинки;
			КонецЕсли;	
			
			НоваяСтрока = Строки.Добавить();
			НоваяСтрока.ИндексКартинки = ИндексКартинки;
			НоваяСтрока.Имя       = ОбъектМетаданных.Имя;
			НоваяСтрока.ПолноеИмя = ТекущаяСтрока.ПолноеИмя + "." + НоваяСтрока.Имя;
			НоваяСтрока.Представление = Представление;
			
			НоваяСтрока.ПолучитьЭлементы().Добавить();
			
		КонецЦикла;
		
	Исключение
	КонецПопытки;	
	
	ТекущаяСтрока.УзелРазвернут = Истина;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьКоллекциюОбъектаМетаданных(Знач ПолноеИмя)
	
	ПолноеИмя = СтрЗаменить(ПолноеИмя, ".", Символы.ПС);
	Уровень   = СтрЧислоСтрок(ПолноеИмя);
	
	ОбъектМетаданных = ПолучитьОбъектМетаданных(ПолноеИмя);
	
	Если Уровень = 2 Тогда
		
		Тип = СтрПолучитьСтроку(ПолноеИмя, 1);
		
		Если Тип = "Справочники"
			ИЛИ Тип = "Документы"
			ИЛИ Тип = "ПланыСчетов"
			ИЛИ Тип = "ПланыВидовХарактеристик"
			ИЛИ Тип = "ПланыВидовРасчета"
			ИЛИ Тип = "БизнесПроцессы"
			ИЛИ Тип = "Задачи"
			ИЛИ Тип = "Обработки" 
			ИЛИ Тип = "Отчеты" Тогда
			
			Массив = Новый Массив;
			Массив.Добавить(Новый Структура("Имя, Представление", "Реквизиты", "Реквизиты"));
			Массив.Добавить(Новый Структура("Имя, Представление", "ТабличныеЧасти", "ТабличныеЧасти"));
			
			Возврат Массив;
			
		ИначеЕсли Тип = "РегистрыСведений"
			ИЛИ Тип = "РегистрыНакопления"
			ИЛИ Тип = "РегистрыНакопления" Тогда
			
			Массив = Новый Массив;
			Массив.Добавить(Новый Структура("Имя, Представление", "Измерения", "Измерения"));
			Массив.Добавить(Новый Структура("Имя, Представление", "Ресурсы", "Ресурсы"));
			Массив.Добавить(Новый Структура("Имя, Представление", "Реквизиты", "Реквизиты"));
			
			Возврат Массив;
			
		КонецЕсли;
		
	ИначеЕсли СтрПолучитьСтроку(ПолноеИмя, Уровень-1) = "ТабличныеЧасти" Тогда
		
		Массив = Новый Массив;
		Массив.Добавить(Новый Структура("Имя, Представление", "Реквизиты", "Реквизиты"));
			
		Возврат Массив;
			
	Иначе
		
		Возврат ОбъектМетаданных;
		
	КонецЕсли;	
	
КонецФункции	

&НаСервереБезКонтекста
Функция ПолучитьОбъектМетаданных(Знач ПолноеИмя) 
	
	ОбъектМетаданных = Метаданные;
	
	Для Счетчик = 1 По СтрЧислоСтрок(ПолноеИмя) Цикл
		
		ОбъектМетаданных = ОбъектМетаданных[СтрПолучитьСтроку(ПолноеИмя, Счетчик)];
		
	КонецЦикла;	
	
	Возврат ОбъектМетаданных;
	
КонецФункции
