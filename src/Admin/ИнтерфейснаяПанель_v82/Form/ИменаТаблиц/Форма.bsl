
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбъектыМетаданных = Новый Массив;
	
	Попытка
		ОбъектыМетаданных.Добавить(ПолучитьГруппуМетаданных(Параметры.Тип)[Параметры.Вид]);
	Исключение
		Отказ = Истина;
		Возврат;
	КонецПопытки;	
	
	Таблица = ПолучитьСтруктуруХраненияБазыДанных(ОбъектыМетаданных, Истина);

	Для каждого СтрокаТЗ Из Таблица Цикл
		
		НоваяСтрока = СтруктураХраненияБазы.ПолучитьЭлементы().Добавить();
		НоваяСтрока.ИмяБД      = СтрокаТЗ.ИмяТаблицыХранения;
		НоваяСтрока.Имя1С      = СтрокаТЗ.Метаданные;
		НоваяСтрока.Метаданные = СтрокаТЗ.Метаданные;
		НоваяСтрока.Назначение = СтрокаТЗ.Назначение;
		
		НоваяСтрокаПоля = НоваяСтрока.ПолучитьЭлементы().Добавить();
		НоваяСтрокаПоля.ИмяБД = "Поля";
		
		Для каждого СтрокаТЗ2 Из СтрокаТЗ.Поля Цикл
			
			НоваяСтрока2 = НоваяСтрокаПоля.ПолучитьЭлементы().Добавить();
			НоваяСтрока2.ИмяБД      = СтрокаТЗ2.ИмяПоляХранения;
			НоваяСтрока2.Имя1С      = СтрокаТЗ2.ИмяПоля;
			НоваяСтрока2.Метаданные = СтрокаТЗ2.Метаданные;
			
		КонецЦикла;
		
		НоваяСтрокаИндексы = НоваяСтрока.ПолучитьЭлементы().Добавить();
		НоваяСтрокаИндексы.ИмяБД = "Индексы";
		
		Для каждого СтрокаТЗ2 Из СтрокаТЗ.Индексы Цикл
			
			НоваяСтрока2 = НоваяСтрокаИндексы.ПолучитьЭлементы().Добавить();
			НоваяСтрока2.ИмяБД      = СтрокаТЗ2.ИмяИндексаХранения;
			
			Для каждого СтрокаТЗ3 Из СтрокаТЗ2.Поля Цикл
				
				НоваяСтрока3 = НоваяСтрока2.ПолучитьЭлементы().Добавить();
				НоваяСтрока3.ИмяБД      = СтрокаТЗ3.ИмяПоляХранения;
				НоваяСтрока3.Имя1С      = СтрокаТЗ3.ИмяПоля;
				НоваяСтрока3.Метаданные = СтрокаТЗ3.Метаданные;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьГруппуМетаданных(Тип)
	
	Если Тип = "ОбщаяФорма" Тогда
		ГруппаМетаданных = Метаданные.ОбщиеФормы;
		
	ИначеЕсли Тип = "РегламентноеЗадание" Тогда
		ГруппаМетаданных = Метаданные.РегламентныеЗадания;
		
	ИначеЕсли Тип = "Константа" Тогда
		ГруппаМетаданных = Метаданные.Константы;
		
	ИначеЕсли Тип = "Справочник" Тогда
		ГруппаМетаданных = Метаданные.Справочники;
		
	ИначеЕсли Тип = "Документ" Тогда
		ГруппаМетаданных = Метаданные.Документы;
		
	ИначеЕсли Тип = "РегистрСведений" Тогда
		ГруппаМетаданных = Метаданные.РегистрыСведений;
		
	ИначеЕсли Тип = "РегистрНакопления" Тогда
		ГруппаМетаданных = Метаданные.РегистрыНакопления;
		
	ИначеЕсли Тип = "РегистрРасчета" Тогда
		ГруппаМетаданных = Метаданные.РегистрыРасчета;
		
	ИначеЕсли Тип = "БизнесПроцесс" Тогда
		ГруппаМетаданных = Метаданные.БизнесПроцессы;
		
	ИначеЕсли Тип = "Задача" Тогда
		ГруппаМетаданных = Метаданные.Задачи;
		
	ИначеЕсли Тип = "Обработка" Тогда
		ГруппаМетаданных = Метаданные.Обработки;
		
	ИначеЕсли Тип = "Отчет" Тогда
		ГруппаМетаданных = Метаданные.Отчеты;
	
	ИначеЕсли Тип = "ПланВидовХарактеристик" Тогда
		ГруппаМетаданных = Метаданные.ПланыВидовХарактеристик;
		
	ИначеЕсли Тип = "ПланВидовРасчета" Тогда
		ГруппаМетаданных = Метаданные.ПланыВидовРасчета;
		
	ИначеЕсли Тип = "ПланСчетов" Тогда
		ГруппаМетаданных = Метаданные.ПланыСчетов;
		
	КонецЕсли;
	
	Возврат ГруппаМетаданных;
	
КонецФункции	

