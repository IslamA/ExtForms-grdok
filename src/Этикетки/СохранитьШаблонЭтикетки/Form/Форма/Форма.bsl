
&НаКлиенте
Процедура Сохранить(Команда)
	
	Адрес = СохранитьНаСервере();
	Описание = Новый ОписаниеПередаваемогоФайла(ИмяФайлаСохранить, Адрес);
	МассивОписаний = Новый Массив;
	МассивОписаний.Добавить(Описание);
	ПолучитьФайлы(МассивОписаний,,,Ложь);
	
	Состояние("Файл сохранен");

КонецПроцедуры

&НаСервере
Функция СохранитьНаСервере()
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("xml");
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.ОткрытьФайл(ИмяВременногоФайла);
	ЗаписатьXML(ЗаписьXML, ШаблонСохранить.ПолучитьОбъект());
	ЗаписьXML.Закрыть();
	
	ДвоичныеДанные = Новый ДвоичныеДанные(ИмяВременногоФайла);
	Адрес = ПоместитьВоВременноеХранилище(ДвоичныеДанные, УникальныйИдентификатор);
	
	Возврат Адрес;
	
КонецФункции

&НаКлиенте
Процедура Загрузить(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВопросЗагрузитьЗавершение", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, "Загрузить шаблон?", РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросЗагрузитьЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ЗагрузитьЗавершение", ЭтотОбъект);
		НачатьПомещениеФайлов(ОписаниеОповещения, , ИмяФайлаЗагрузить , Ложь, Новый УникальныйИдентификатор);
	КонецЕсли;	
	
КонецПроцедуры


&НаКлиенте
Процедура ЗагрузитьЗавершение(ПомещенныеФайлы, ДополнительныеПараметры) Экспорт
	
    Если ПомещенныеФайлы = Неопределено Тогда
        Возврат;
	КонецЕсли;
	
	ЗагрузитьНаСервере(ПомещенныеФайлы[0].Хранение);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНаСервере(АдресХранилища)
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("xml");
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресХранилища);
    ДвоичныеДанные.Записать(ИмяВременногоФайла);
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьФайл(ИмяВременногоФайла);
	Если ВозможностьЧтенияXML(ЧтениеXML) Тогда
		НовыйОбъект = ПрочитатьXML(ЧтениеXML);
		
		ТекущийОбъект = ШаблонЗагрузить.ПолучитьОбъект();
		ЗаполнитьЗначенияСвойств(ТекущийОбъект, НовыйОбъект,, "Ссылка, Родитель, Владелец, ПометкаУдаления, Наименование, ДляЧего");
		ТекущийОбъект.Записать();
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаСохранитьНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ИмяФайлаСохранитьНачалоВыбораЗавершение", ЭтотОбъект);
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.Фильтр = "*.xml|*.xml";
	Диалог.Показать(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаСохранитьНачалоВыбораЗавершение(ВыбФайлы, ДопПараметры) Экспорт
	
	Если ВыбФайлы <> Неопределено Тогда
		ИмяФайлаСохранить = ВыбФайлы[0];
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаСохранитьОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЗапуститьПриложение(ИмяФайлаСохранить);
	
КонецПроцедуры


&НаКлиенте
Процедура ИмяФайлаЗагрузитьНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ИмяФайлаЗагрузитьНачалоВыбораЗавершение", ЭтотОбъект);
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Фильтр = "*.xml|*.xml";
	Диалог.Показать(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаЗагрузитьНачалоВыбораЗавершение(ВыбФайлы, ДопПараметры) Экспорт
	
	Если ВыбФайлы <> Неопределено Тогда
		ИмяФайлаЗагрузить = ВыбФайлы[0];
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаЗагрузитьНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	Элемент.СписокВыбора.Очистить();
	Элемент.СписокВыбора.Добавить(ИмяФайлаСохранить);
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаЗагрузитьОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЗапуститьПриложение(ИмяФайлаЗагрузить);
	
КонецПроцедуры


