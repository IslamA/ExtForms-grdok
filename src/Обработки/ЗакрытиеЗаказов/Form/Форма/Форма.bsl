
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект.Период = НачалоМесяца(НачалоМесяца(ТекущаяДата()) - 1);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПредставлениеПериодаРегистрации = ОбщегоНазначенияУТКлиент.ПолучитьПредставлениеПериодаРегистрации(Объект.Период);
	
КонецПроцедуры

#КонецОбласти

#Область ОбщиеПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодаРегистрацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Оповещение = Новый ОписаниеОповещения("ПредставлениеПериодаРегистрацииНачалоВыбораЗавершение", ЭтотОбъект);
	ОбщегоНазначенияУТКлиент.НачалоВыбораПредставленияПериодаРегистрации(Элемент,
		СтандартнаяОбработка,
		Объект.Период,
		ЭтаФорма,
		Оповещение);
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодаРегистрацииНачалоВыбораЗавершение(ВыбранныйПериод, ДополнительныеПараметры) Экспорт 
	Если ВыбранныйПериод <> Неопределено Тогда
		Объект.Период = ВыбранныйПериод;
		ПредставлениеПериодаРегистрации = ОбщегоНазначенияУТКлиент.ПолучитьПредставлениеПериодаРегистрации(Объект.Период);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодаРегистрацииРегулирование(Элемент, Направление, СтандартнаяОбработка)
	ОбщегоНазначенияУТКлиент.РегулированиеПредставленияПериодаРегистрации(Направление,
		СтандартнаяОбработка,
		Объект.Период,
		ПредставлениеПериодаРегистрации);
КонецПроцедуры

#КонецОбласти

#Область МаршрутныеЛисты

&НаКлиенте
Процедура МЛВклВсе(Команда)
	
	Для каждого СтрокаТЗ из ТаблицаМаршрутныеЛисты Цикл
		СтрокаТЗ.Пометка = Истина;
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура МЛВыклВсе(Команда)
	
	Для каждого СтрокаТЗ из ТаблицаМаршрутныеЛисты Цикл
		СтрокаТЗ.Пометка = Ложь;
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаМаршрутныеЛистыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Имя = СтрЗаменить(Поле.Имя, "ТаблицаМаршрутныеЛисты", "");
	
	СтрокаТЗ = ТаблицаМаршрутныеЛисты.НайтиПоИдентификатору(ВыбраннаяСтрока);
	ПоказатьЗначение(, СтрокаТЗ[Имя]);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьМЛ(Команда)
	ЗаполнитьМЛНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьМЛНаСервере()
	
	Запрос = Новый ПостроительЗапроса;
	Запрос.Параметры.Вставить("Организация", Объект.Организация);
	Запрос.Параметры.Вставить("Дата1",       НачалоМесяца(Объект.Период));
	Запрос.Параметры.Вставить("Дата2",       КонецМесяца(Объект.Период));
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ИСТИНА КАК Пометка,
	|	Док.Ссылка КАК Документ,
	|	Док.Распоряжение КАК Заказ,
	|	Док.Подразделение
	|ИЗ
	|	Документ.МаршрутныйЛистПроизводства КАК Док
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ДокВыходныеИзделия.Ссылка КАК Ссылка,
	|			СУММА(ДокВыходныеИзделия.КоличествоФакт) КАК КоличествоФакт
	|		ИЗ
	|			Документ.МаршрутныйЛистПроизводства.ВыходныеИзделия КАК ДокВыходныеИзделия
	|		ГДЕ
	|			ДокВыходныеИзделия.Ссылка.Распоряжение.Дата МЕЖДУ &Дата1 И &Дата2
	|			И ДокВыходныеИзделия.Ссылка.Проведен
	|			И ДокВыходныеИзделия.Ссылка.Организация = &Организация
	|			И ДокВыходныеИзделия.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыМаршрутныхЛистовПроизводства.Выполнен)
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ДокВыходныеИзделия.Ссылка) КАК ДокВыходныеИзделия
	|		ПО (ДокВыходныеИзделия.Ссылка = Док.Ссылка)
	|ГДЕ
	|	Док.Распоряжение.Дата МЕЖДУ &Дата1 И &Дата2
	|	И Док.Проведен
	|	И Док.Организация = &Организация
	|	И Док.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыМаршрутныхЛистовПроизводства.Выполнен)
	|	И ЕСТЬNULL(ДокВыходныеИзделия.КоличествоФакт, 0) = 0
	|{ГДЕ
	|	Док.Распоряжение.Подразделение.* КАК Подразделение}
	|
	|УПОРЯДОЧИТЬ ПО
	|	Док.Дата";
	
	Если ЗначениеЗаполнено(Объект.Подразделение) Тогда
		
		ЭлементОтбора = Запрос.Отбор.Добавить("Подразделение");
		ЭлементОтбора.Значение      = Объект.Подразделение;
		ЭлементОтбора.ВидСравнения  = ВидСравнения.ВИерархии;
		ЭлементОтбора.Использование = Истина;
		
	КонецЕсли;	
	
	Запрос.Выполнить();
	
	Таблица = Запрос.Результат.Выгрузить();
	ТаблицаМаршрутныеЛисты.Загрузить(Таблица);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьМЛ(Команда)
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ОтменитьМЛОтвет", ЭтотОбъект), "Отменить маршрутные листы?", РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьМЛОтвет(Ответ, ДопПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		Состояние("Отмена маршрутных листов");
		ОтменитьМЛСервер();
		Состояние("Маршрутные листы отменены");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтменитьМЛСервер()
	
	Для каждого СтрокаТЗ из ТаблицаМаршрутныеЛисты Цикл
		
		Если НЕ СтрокаТЗ.Пометка Тогда
			Продолжить;
		КонецЕсли;	
		
		ДокументОбъект = СтрокаТЗ.Документ.ПолучитьОбъект();
		ДокументОбъект.Статус = Перечисления.СтатусыМаршрутныхЛистовПроизводства.Отменен;
		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ЗаказыНаПроизводство

&НаКлиенте
Процедура ДеревоЗаказыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СтрокаТЗ = ДеревоЗаказы.НайтиПоИдентификатору(ВыбраннаяСтрока);
	ПоказатьЗначение(, СтрокаТЗ.Заказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаказыВклВсе(Команда)
	
	Для каждого Строка1 из ДеревоЗаказы.ПолучитьЭлементы() Цикл
		Строка1.Пометка = Истина;
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаказыВыклВсе(Команда)
	
	Для каждого Строка1 из ДеревоЗаказы.ПолучитьЭлементы() Цикл
		Строка1.Пометка = Ложь;
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЗаказы(Команда)
	
	ЗаполнитьЗаказыНаСервере();
	
	Для каждого Строка1 из ДеревоЗаказы.ПолучитьЭлементы() Цикл
		Элементы.ДеревоЗаказы.Развернуть(Строка1.ПолучитьИдентификатор());
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗаказыНаСервере()
	
	Запрос = Новый ПостроительЗапроса;
	Запрос.Параметры.Вставить("Организация", Объект.Организация);
	Запрос.Параметры.Вставить("Дата1",       НачалоМесяца(Объект.Период));
	Запрос.Параметры.Вставить("Дата2",       КонецМесяца(Объект.Период));
	Запрос.Параметры.Вставить("ПустойКлюч",  Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000"));
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Выборка.Заказ КАК Заказ,
	|	Выборка.КодСтроки,
	|	Выборка.Номенклатура,
	|	Выборка.Характеристика,
	|	Выборка.Назначение,
	|	Выборка.КоличествоЗаказ КАК КоличествоЗаказ,
	|	Выборка.КоличествоВыполненоПлан КАК КоличествоВыполненоПлан,
	|	Выборка.КоличествоВыполненоФакт КАК КоличествоВыполненоФакт,
	|	ДокПродукция.КлючСвязи КАК КлючСвязиПродукция,
	|	&ПустойКлюч КАК КлючСвязиПолуфабрикат
	|ИЗ
	|	(ВЫБРАТЬ
	|		Выборка.Заказ КАК Заказ,
	|		Выборка.КодСтроки КАК КодСтроки,
	|		Выборка.Номенклатура КАК Номенклатура,
	|		Выборка.Характеристика КАК Характеристика,
	|		Выборка.Назначение КАК Назначение,
	|		СУММА(Выборка.КоличествоЗаказ) КАК КоличествоЗаказ,
	|		СУММА(Выборка.КоличествоВыполненоПлан) КАК КоличествоВыполненоПлан,
	|		СУММА(Выборка.КоличествоВыполненоФакт) КАК КоличествоВыполненоФакт
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ДокПродукция.КодСтроки КАК КодСтроки,
	|			Заказ.Ссылка КАК Заказ,
	|			Заказ.Номенклатура КАК Номенклатура,
	|			Заказ.Характеристика КАК Характеристика,
	|			Заказ.Назначение КАК Назначение,
	|			Заказ.Количество КАК КоличествоЗаказ,
	|			0 КАК КоличествоВыполненоПлан,
	|			0 КАК КоличествоВыполненоФакт
	|		ИЗ
	|			Документ.ЗаказНаПроизводство.ВыходныеИзделия КАК Заказ
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК ДокПродукция
	|				ПО Заказ.КлючСвязиПродукция = ДокПродукция.КлючСвязи
	|					И Заказ.Ссылка = ДокПродукция.Ссылка
	|		ГДЕ
	|			Заказ.Ссылка.Дата МЕЖДУ &Дата1 И &Дата2
	|			И Заказ.Ссылка.Проведен
	|			И Заказ.Ссылка.Организация = &Организация
	|		{ГДЕ
	|			Заказ.Ссылка.Подразделение.* КАК Подразделение}
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			МЛ.Ссылка.КодСтроки,
	|			МЛ.Ссылка.Распоряжение,
	|			МЛ.Номенклатура,
	|			МЛ.Характеристика,
	|			МЛ.Назначение,
	|			0,
	|			МЛ.Количество,
	|			МЛ.КоличествоФакт
	|		ИЗ
	|			Документ.МаршрутныйЛистПроизводства.ВыходныеИзделия КАК МЛ
	|		ГДЕ
	|			МЛ.Ссылка.Распоряжение.Дата МЕЖДУ &Дата1 И &Дата2
	|			И МЛ.Ссылка.Проведен
	|			И МЛ.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыМаршрутныхЛистовПроизводства.Выполнен)
	|		{ГДЕ
	|			МЛ.Ссылка.Распоряжение.Подразделение.* КАК Подразделение}) КАК Выборка
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Выборка.Заказ,
	|		Выборка.КодСтроки,
	|		Выборка.Номенклатура,
	|		Выборка.Характеристика,
	|		Выборка.Назначение
	|	
	//|	ИМЕЮЩИЕ
	//|		СУММА(Выборка.КоличествоЗаказ) - СУММА(Выборка.КоличествоВыполненоПлан) <> 0 ИЛИ СУММА(Выборка.КоличествоВыполненоФакт) = 0
	|) КАК Выборка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК ДокПродукция
	|		ПО Выборка.Заказ = ДокПродукция.Ссылка
	|			И Выборка.КодСтроки = ДокПродукция.КодСтроки
	|
	|УПОРЯДОЧИТЬ ПО
	|	Выборка.Заказ,
	|	Выборка.КодСтроки
	|ИТОГИ
	|	СУММА(КоличествоЗаказ),
	|	СУММА(КоличествоВыполненоПлан),
	|	СУММА(КоличествоВыполненоФакт)
	|ПО
	|	Заказ
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Если ЗначениеЗаполнено(Объект.Подразделение) Тогда
		
		ЭлементОтбора = Запрос.Отбор.Добавить("Подразделение");
		ЭлементОтбора.Значение      = Объект.Подразделение;
		ЭлементОтбора.ВидСравнения  = ВидСравнения.ВИерархии;
		ЭлементОтбора.Использование = Истина;
		
	КонецЕсли;	
	
	Запрос.Выполнить();
	
	Дерево = Запрос.Результат.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	ДеревоЗаказы.ПолучитьЭлементы().Очистить();
	
	Для каждого Строка1 из Дерево.Строки Цикл
		
		НоваяСтрока1 = ДеревоЗаказы.ПолучитьЭлементы().Добавить();	
		ЗаполнитьЗначенияСвойств(НоваяСтрока1, Строка1);
		НоваяСтрока1.Представление = Строка(Строка1.Заказ);
		НоваяСтрока1.Уровень       = 0;
		НоваяСтрока1.Пометка       = Истина;
		
		Для каждого Строка2 из Строка1.Строки Цикл
			
			НоваяСтрока2 = НоваяСтрока1.ПолучитьЭлементы().Добавить();	
			ЗаполнитьЗначенияСвойств(НоваяСтрока2, Строка2);
			НоваяСтрока2.Уровень = 1;
			
			НоваяСтрока2.Представление = Строка(Строка2.Номенклатура);
			Если ЗначениеЗаполнено(Строка2.Характеристика) Тогда
				НоваяСтрока2.Представление = НоваяСтрока2.Представление + ", " + Строка(Строка2.Характеристика);
			КонецЕсли;	
			
			Если ЗначениеЗаполнено(Строка2.Назначение) Тогда
				НоваяСтрока2.Представление = НоваяСтрока2.Представление + ", " + Строка(Строка2.Назначение);
			КонецЕсли;	
			
		КонецЦикла;
		
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура СократитьПроизводство(Команда)
	ПоказатьВопрос(Новый ОписаниеОповещения("СократитьПроизводствоОтвет", ЭтотОбъект), "Сократить производство?", РежимДиалогаВопрос.ДаНет);
КонецПроцедуры

&НаКлиенте
Процедура СократитьПроизводствоОтвет(Ответ, ДопПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		Состояние("Сокращение производства");
		СократитьПроизводствоНаСервере();
		Состояние("Производство сокращено");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СократитьПроизводствоНаСервере()
	
	Для каждого Строка1 из ДеревоЗаказы.ПолучитьЭлементы() Цикл
		
		Если НЕ Строка1.Пометка Тогда
			Продолжить;
		КонецЕсли;
		
		СписокЗаказов = Новый СписокЗначений;
		СписокЗаказов.Добавить(Строка1.Заказ);
		
		СписокКодовСтрокПродукции = Новый СписокЗначений;
		
		Для каждого Строка2 из Строка1.ПолучитьЭлементы() Цикл
			СписокКодовСтрокПродукции.Добавить(Строка2.КодСтроки);
		КонецЦикла;
		
		// Получение данных для сокращения
		ПараметрыЗадания = Новый Структура;
		
		ПараметрыЗадания.Вставить("СписокЗаказов", СписокЗаказов);
		ПараметрыЗадания.Вставить("СписокКодовСтрокПродукции", СписокКодовСтрокПродукции);
		
		ПараметрыЗадания.Вставить("ТекущаяДатаСеанса", ТекущаяДатаСеанса());
	
		АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
		Обработки.СокращениеПроизводства.СостояниеЗаказовНаПроизводство(ПараметрыЗадания, АдресХранилища);
		
		Для каждого Строка2 из Строка1.ПолучитьЭлементы() Цикл
			
			// Сокращение производства
			ПараметрыСокращения = Новый Структура; 
			ПараметрыСокращения.Вставить("Заказ",                 Строка1.Заказ);
			ПараметрыСокращения.Вставить("КлючСвязиПродукция",    Строка2.КлючСвязиПродукция);
			ПараметрыСокращения.Вставить("КлючСвязиПолуфабрикат", Строка2.КлючСвязиПолуфабрикат);
			ПараметрыСокращения.Вставить("НовоеКоличество",       Строка2.КоличествоВыполненоФакт);
			ПараметрыСокращения.Вставить("КоличествоТребуется",   Строка2.КоличествоВыполненоФакт);
			ПараметрыСокращения.Вставить("БезопасноеСокращение",  Строка2.КоличествоВыполненоФакт);
			ПараметрыСокращения.Вставить("ДоделатьНачатыеПолуфабрикаты", Истина);
			
			ПараметрыОбработки = Новый Структура;
			ПараметрыОбработки.Вставить("АдресХранилища",          АдресХранилища);
			ПараметрыОбработки.Вставить("ПараметрыСокращения",     ПараметрыСокращения);
			ПараметрыОбработки.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
			
			Обработки.СокращениеПроизводства.СократитьПроизводство(ПараметрыОбработки);
			
			СтруктураДанных = ПолучитьИзВременногоХранилища(ПараметрыОбработки.АдресХранилища);
			
		КонецЦикла;
		
		ЗаказОбъект = Строка1.Заказ.ПолучитьОбъект();
		Если Ложь Тогда
			ЗаказОбъект = Документы.ЗаказНаПроизводство.СоздатьДокумент();
		КонецЕсли;
		
	КонецЦикла;	
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиКодСтрокиЗаказа(Заказ, ПараметрыСтроки)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДокПродукция.КодСтроки
	|ИЗ
	|	Документ.ЗаказНаПроизводство.ВыходныеИзделия КАК ДокВыходныеИзделия
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК ДокПродукция
	|		ПО ДокВыходныеИзделия.КлючСвязиПродукция = ДокПродукция.КлючСвязи
	|			И ДокВыходныеИзделия.Ссылка = ДокПродукция.Ссылка
	|ГДЕ
	|	ДокВыходныеИзделия.Ссылка = &Ссылка
	|	И ДокВыходныеИзделия.Номенклатура = &Номенклатура
	|	И ДокВыходныеИзделия.Характеристика = &Характеристика
	|	И ДокВыходныеИзделия.Назначение = &Назначение";
	                                     
	Запрос.Параметры.Вставить("Ссылка",         Заказ);
	Запрос.Параметры.Вставить("Номенклатура",   ПараметрыСтроки.Номенклатура);
	Запрос.Параметры.Вставить("Характеристика", ПараметрыСтроки.Характеристика);
	Запрос.Параметры.Вставить("Назначение",     ПараметрыСтроки.Назначение);
	
	Таблица = Запрос.Выполнить().Выгрузить();
	Возврат Таблица.ВыгрузитьКолонку("КодСтроки");
	
КонецФункции	

#КонецОбласти
