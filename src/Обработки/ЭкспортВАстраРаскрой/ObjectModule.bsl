#Область ПрограмныйИнтерфейс

Функция СведенияОВнешнейОбработке() Экспорт
	
	ПараметрыРегистрации = Новый Структура;
	МассивНазначений = Новый Массив;
	
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке("2.2.2.1");
	ПараметрыРегистрации.Вид          = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиДополнительнаяОбработка();
	ПараметрыРегистрации.Наименование = Метаданные().Представление();
	ПараметрыРегистрации.Версия       = Метаданные().Комментарий;
	ПараметрыРегистрации.Назначение   = МассивНазначений;
	ПараметрыРегистрации.БезопасныйРежим = Ложь;
	
	ДобавитьКоманду(ПараметрыРегистрации.Команды, ПараметрыРегистрации.Наименование, ПараметрыРегистрации.Наименование, ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыОткрытиеФормы(), Истина, "ПечатьMXL");
		
	Возврат ПараметрыРегистрации;	
		
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
	
	НоваяКоманда = ТаблицаКоманд.Добавить();
	НоваяКоманда.Представление = Представление;
	НоваяКоманда.Идентификатор = Идентификатор;
	НоваяКоманда.Использование = Использование;
	НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
	НоваяКоманда.Модификатор = Модификатор;
	
КонецПроцедуры

#КонецОбласти

Функция ПолучитьОстатки(Номенклатура, Склад) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("Номенклатура", Номенклатура);
	Запрос.Параметры.Вставить("Склад",        Склад);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИСТИНА КАК Пометка,
	|	Рег.Номенклатура КАК Номенклатура,
	|	Рег.Характеристика КАК Характеристика,
	|	Рег.ВНаличииОстаток КАК Количество
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки(
	|			,
	|			Номенклатура В (&Номенклатура)
	|				И Склад = &Склад) КАК Рег
	|
	|УПОРЯДОЧИТЬ ПО
	|	Номенклатура,
	|	Характеристика";
	
	Таблица = Запрос.Выполнить().Выгрузить();
	
	СтрокиКУдалению = Новый Массив;
	Для каждого СтрокаТЗ из Таблица Цикл
		
		Толщина = _ХарактеристикиНоменклатуры.ПолучитьТолщину(СтрокаТЗ.Характеристика);
		Радиус  = _ХарактеристикиНоменклатуры.ПолучитьРадиус(СтрокаТЗ.Характеристика);
		
		Если (ОтборТолщина И Толщина <> ОтборТолщинаЗначение)
			ИЛИ (ОтборРадиус И Радиус <> ОтборРадиусЗначение) Тогда
			СтрокиКУдалению.Добавить(СтрокаТЗ);
			
		КонецЕсли;	
		
	КонецЦикла;	
	
	Для каждого СтрокаТЗ из СтрокиКУдалению Цикл
		Таблица.Удалить(СтрокаТЗ);
	КонецЦикла;	
	
	Возврат Таблица;
	
КонецФункции	

Функция ВыгрузитьОстатки() Экспорт
	
	Свойства = ПолучитьСтруктуруСвойств();
	
	ИмяФайлаXML = ПолучитьИмяВременногоФайла("xml");
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.ОткрытьФайл(ИмяФайлаXML);
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("data");
	ЗаписьXML.ЗаписатьАтрибут("stock_plate_resete",  "1");
	ЗаписьXML.ЗаписатьАтрибут("stock_rest_resete",   "1");
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("data_sheet");
	ЗаписьXML.ЗаписатьНачалоЭлемента("list_materials");
	
	ПредНоменклатура = Неопределено;
	Для каждого Строка1 из ТаблицаОстатков Цикл
		
		Если НЕ ЗначениеЗаполнено(Строка1.Толщина)
			ИЛИ Строка1.КоличествоЛистов = 0
			ИЛИ НЕ Строка1.Пометка Тогда
			
			Продолжить;
			
		КонецЕсли;	
		
		ТекНоменклатура = Строка1.Наименование;
		
		Если ТекНоменклатура <> ПредНоменклатура Тогда
			
			Если ПредНоменклатура <> Неопределено Тогда
				ЗаписьXML.ЗаписатьКонецЭлемента(); // list_sheets
				ЗаписьXML.ЗаписатьКонецЭлемента(); // material
			КонецЕсли;	
			
			ЗаписьXML.ЗаписатьНачалоЭлемента("material");
			ЗаписьXML.ЗаписатьАтрибут("name",    ТекНоменклатура);
			ЗаписьXML.ЗаписатьАтрибут("wid_cut", Формат(ШиринраРеза, "ЧРД=.; ЧГ=0"));
			
			КромкаСтр = "0;0;" + Формат(Кромка, "ЧРД=.; ЧГ=0") + ";" + Формат(Кромка, "ЧРД=.; ЧГ=0");
			ЗаписьXML.ЗаписатьАтрибут("sheet_border", КромкаСтр);
			ЗаписьXML.ЗаписатьАтрибут("rest_border",  КромкаСтр);
			
			ЗаписьXML.ЗаписатьАтрибут("rest_size",  Формат(МинОстатокДлина, "ЧРД=.; ЧГ=0") + ";" + Формат(МинОстатокШирина, "ЧРД=.; ЧГ=0"));
		
			ЗаписьXML.ЗаписатьНачалоЭлемента("list_sheets");
			
			ПредНоменклатура = ТекНоменклатура;
			
		КонецЕсли;	
		
		ЗаписьXML.ЗаписатьНачалоЭлемента("sheet");
		ЗаписьXML.ЗаписатьАтрибут("length",   Формат(Строка1.Длина, "ЧН=0; ЧГ=0"));
		ЗаписьXML.ЗаписатьАтрибут("width",    Формат(Строка1.Ширина, "ЧН=0; ЧГ=0"));
		ЗаписьXML.ЗаписатьАтрибут("thick",    Формат(Строка1.Толщина, "ЧН=0; ЧГ=0"));
		ЗаписьXML.ЗаписатьАтрибут("quantity", Формат(Строка1.КоличествоЛистов, "ЧГ=0"));
		
		Если НоменклатураЛистыОтмеченные.НайтиПоЗначению(Строка1.Номенклатура) <> Неопределено Тогда
			ЗаписьXML.ЗаписатьАтрибут("type", "0");
		Иначе	
			ЗаписьXML.ЗаписатьАтрибут("type", "1");
		КонецЕсли;
		
		ЗаписьXML.ЗаписатьКонецЭлемента(); // sheet
			
	КонецЦикла;
	
	Если ПредНоменклатура <> Неопределено Тогда
		ЗаписьXML.ЗаписатьКонецЭлемента(); // list_sheets
		ЗаписьXML.ЗаписатьКонецЭлемента(); // material
	КонецЕсли;	
	
	ЗаписьXML.ЗаписатьКонецЭлемента(); // list_materials
	ЗаписьXML.ЗаписатьКонецЭлемента(); // data_sheet
	ЗаписьXML.ЗаписатьКонецЭлемента(); // data
	
	ЗаписьXML.Закрыть();
	
	Возврат ИмяФайлаXML;
	
КонецФункции

Функция Выгрузить() Экспорт
	
	ИмяФайлаXML = ВыгрузитьОстатки();
	Возврат ИмяФайлаXML;
	
КонецФункции	

Функция вЧисло(Парам)
	
	Попытка
		Возврат Число(Строка(Парам));
	Исключение
		Возврат 0;
	КонецПопытки;	
	
КонецФункции	

Процедура ЗаполнитьОстатки() Экспорт
	
	Номенклатура = Новый Массив;
	Для каждого ЭлементСписка из НоменклатураОстаткиОтмеченные Цикл
		Номенклатура.Добавить(ЭлементСписка.Значение);
	КонецЦикла;	
	
	Таблица = ПолучитьОстатки(Номенклатура, Склад);
	
	Свойства = ПолучитьСтруктуруСвойств();
	
	Для каждого Строка1 из Таблица Цикл
		
		ЗначенияСвойств = ПолучитьЗначениеСвойств(Строка1.Номенклатура, Строка1.Характеристика, Свойства);
		
		Если (ОтборТолщина И ЗначенияСвойств.Толщина <> ОтборТолщинаЗначение)
			ИЛИ (ОтборРадиус И ЗначенияСвойств.Радиус <> ОтборРадиусЗначение) Тогда
			Продолжить;
		КонецЕсли;	
			
		НоваяСтрока = ТаблицаОстатков.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка1);
		
		НоваяСтрока.Длина   = ЗначенияСвойств.Длина;
		НоваяСтрока.Ширина  = ЗначенияСвойств.Ширина;
		НоваяСтрока.Толщина = ЗначенияСвойств.Толщина;
		НоваяСтрока.Радиус  = ЗначенияСвойств.Радиус;
		
		НоваяСтрока.КоличествоЛистов = ПолучитьКоличествоЛистов(Строка1.Номенклатура, Строка1.Характеристика, ЗначенияСвойств, Строка1.Количество);
		НоваяСтрока.Наименование = ПолучитьНаименованиеНоменклатуры(Строка1.Номенклатура, ЗначенияСвойств);
		
		Если НЕ ЗначениеЗаполнено(ЗначенияСвойств.Толщина) Тогда
			НоваяСтрока.Пометка = Ложь;
		КонецЕсли;	
		
	КонецЦикла;
	
КонецПроцедуры	

Процедура ЗаполнитьЛисты() Экспорт
	
	Свойства = ПолучитьСтруктуруСвойств();
	
	Если НеВыгружатьКоличествоЛисты Тогда
		
		МассивРадиусов = Новый Массив;
		МассивРадиусов.Добавить(3500);
		
		МассивТолщин = Новый Массив;
		МассивТолщин.Добавить(0);
		
		Запрос = Новый Запрос;
		Запрос.Параметры.Вставить("Номенклатура",    НоменклатураЛистыОтмеченные);
		Запрос.Параметры.Вставить("СвойствоНаименование",  Свойства.Наименование);
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(СпрНаименование.Значение КАК СТРОКА(250)) КАК Наименование,
		|	Спр.Владелец КАК Номенклатура,
		|	Спр.Ссылка КАК Характеристика
		|ИЗ
		|	Справочник.ХарактеристикиНоменклатуры КАК Спр
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура.ДополнительныеРеквизиты КАК СпрНаименование
		|		ПО Спр.Владелец = СпрНаименование.Ссылка
		|			И (СпрНаименование.Свойство = &СвойствоНаименование)
		|ГДЕ
		|	Спр.Владелец В(&Номенклатура)
		|	И НЕ Спр.ПометкаУдаления";
		
		Таблица = Запрос.Выполнить().Выгрузить();
		Таблица.Колонки.Добавить("Толщина");
		Таблица.Колонки.Добавить("Радиус");
		
		СтрокиКУдалению = Новый Массив;
		
		Для каждого СтрокаТЗ из Таблица Цикл
			
			СтрокаТЗ.Толщина = _ХарактеристикиНоменклатуры.ПолучитьТолщину(СтрокаТЗ.Характеристика);
			СтрокаТЗ.Радиус  = _ХарактеристикиНоменклатуры.ПолучитьРадиус(СтрокаТЗ.Характеристика);
			
			Если МассивРадиусов.Найти(СтрокаТЗ.Радиус) <> Неопределено Тогда
				СтрокиКУдалению.Добавить(СтрокаТЗ);
				
			ИначеЕсли МассивТолщин.Найти(СтрокаТЗ.Толщина) <> Неопределено Тогда
				СтрокиКУдалению.Добавить(СтрокаТЗ);
				
			ИначеЕсли Цел(СтрокаТЗ.Толщина) <> СтрокаТЗ.Толщина Тогда
				СтрокиКУдалению.Добавить(СтрокаТЗ);
			
			ИначеЕсли (ОтборТолщина И СтрокаТЗ.Толщина <> ОтборТолщинаЗначение)
				ИЛИ (ОтборРадиус И СтрокаТЗ.Радиус <> ОтборРадиусЗначение) Тогда
				СтрокиКУдалению.Добавить(СтрокаТЗ);
				
			КонецЕсли;	
			
		КонецЦикла;	
		
		Для каждого СтрокаТЗ из СтрокиКУдалению Цикл
			Таблица.Удалить(СтрокаТЗ);
		КонецЦикла;	
		
		Таблица.Свернуть("Номенклатура, Наименование, Толщина, Радиус", "");
		
	Иначе	
		
		Номенклатура = Новый Массив;
		Для каждого ЭлементСписка из НоменклатураЛистыОтмеченные Цикл
			Номенклатура.Добавить(ЭлементСписка.Значение);
		КонецЦикла;	
		
		Таблица = ПолучитьОстатки(Номенклатура, Склад);
		
	Конецесли;	
	
	Для каждого Строка1 из Таблица Цикл
		
		НоваяСтрока = ТаблицаОстатков.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка1);
		
		Если НеВыгружатьКоличествоЛисты Тогда
			
			ЗначенияСвойств = Новый Структура;
			ЗначенияСвойств.Вставить("Наименование", Строка1.Наименование);
			ЗначенияСвойств.Вставить("Длина",    1620);
			ЗначенияСвойств.Вставить("Ширина",   800);
			ЗначенияСвойств.Вставить("Толщина",  вЧисло(Строка1.Толщина));
			ЗначенияСвойств.Вставить("Радиус",   вЧисло(Строка1.Радиус));
			ЗначенияСвойств.Вставить("Сорт",     "");
			ЗначенияСвойств.Вставить("Покрытие", "");
			
			НоваяСтрока.Длина   = ЗначенияСвойств.Длина;
			НоваяСтрока.Ширина  = ЗначенияСвойств.Ширина;
			НоваяСтрока.Толщина = ЗначенияСвойств.Толщина;
			НоваяСтрока.Радиус  = ЗначенияСвойств.Радиус;
			НоваяСтрока.КоличествоЛистов = 1000000;
			
		Иначе	
			
			ЗначенияСвойств = ПолучитьЗначениеСвойств(Строка1.Номенклатура, Строка1.Характеристика, Свойства);
			НоваяСтрока.Длина   = ЗначенияСвойств.Длина;
			НоваяСтрока.Ширина  = ЗначенияСвойств.Ширина;
			НоваяСтрока.Толщина = ЗначенияСвойств.Толщина;
			НоваяСтрока.Радиус  = ЗначенияСвойств.Радиус;
		
			НоваяСтрока.КоличествоЛистов = ПолучитьКоличествоЛистов(Строка1.Номенклатура, Строка1.Характеристика, ЗначенияСвойств, Строка1.Количество);
		КонецЕсли;	
		НоваяСтрока.Наименование = ПолучитьНаименованиеНоменклатуры(Строка1.Номенклатура, ЗначенияСвойств);
		
		НоваяСтрока.Пометка = ЗначениеЗаполнено(НоваяСтрока.Толщина);
		
	КонецЦикла;
	
КонецПроцедуры	

Процедура Заполнить() Экспорт
	
	ТаблицаОстатков.Очистить();
	ЗаполнитьОстатки();
	ЗаполнитьЛисты();
	
	ТаблицаОстатков.Сортировать("Номенклатура, Радиус, Толщина, Длина");
	
КонецПроцедуры	


Функция ПолучитьСтруктуруСвойств()
	
	Свойства = Новый Структура;
	Свойства.Вставить("Наименование", ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоНаименованию("Описание товара RUS (Номенклатура)"));
	
	Свойства.Вставить("Покрытие", ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоНаименованию("5 Покрытие (Характеристики номенклатуры)"));
	Свойства.Вставить("Сорт",     ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоНаименованию("6 Сорт (Характеристики номенклатуры)"));
	
	Возврат Свойства;
	
КонецФункции	

Функция ПолучитьЗначениеСвойств(Номенклатура, Характеристика, Свойства)
	
	ЗначенияСвойств = Новый Структура;
	
	ЗначенияСвойств.Вставить("Наименование", _ОбщегоНазначенияДоп.ПолучитьДопРеквизит(Номенклатура, Свойства.Наименование));
	
	ЗначенияСвойств.Вставить("Длина",     _ХарактеристикиНоменклатуры.ПолучитьДлину(Характеристика));
	ЗначенияСвойств.Вставить("Ширина",    _ХарактеристикиНоменклатуры.ПолучитьШирину(Характеристика));
	ЗначенияСвойств.Вставить("Толщина",   _ХарактеристикиНоменклатуры.ПолучитьТолщину(Характеристика));
	ЗначенияСвойств.Вставить("Радиус",    _ХарактеристикиНоменклатуры.ПолучитьРадиус(Характеристика));
	
	ЗначенияСвойств.Вставить("Покрытие",  _ОбщегоНазначенияДоп.ПолучитьДопРеквизит(Характеристика, Свойства.Покрытие));
	ЗначенияСвойств.Вставить("Сорт",      _ОбщегоНазначенияДоп.ПолучитьДопРеквизит(Характеристика, Свойства.Сорт));
		
	Возврат ЗначенияСвойств;
	
КонецФункции

Функция ПолучитьНаименованиеНоменклатуры(Номенклатура, ЗначенияСвойств) Экспорт
	
	Сорт = ЗначенияСвойств.Сорт;
	Если НЕ ЗначениеЗаполнено(Сорт) Тогда
		Сорт = СортПоУмолчанию;
	КонецЕсли;	
	
	Покрытие = ЗначенияСвойств.Покрытие;
	Если НЕ ЗначениеЗаполнено(Покрытие) Тогда
		Покрытие = "g/g";
	КонецЕсли;	
	
	Наименование = Строка(ЗначенияСвойств.Наименование) + " " + Формат(ЗначенияСвойств.Радиус, "ЧГ=0") + ", " + СокрЛП(Покрытие) + ", " + СокрЛП(Сорт);
		
	Возврат Наименование;
	
КонецФункции

Функция ПолучитьКоличествоЛистов(Номенклатура, Характеристика, ЗначенияСвойств, Количество)
	
	Попытка
		Возврат Окр(Количество / ((ЗначенияСвойств.Длина/1000) * (ЗначенияСвойств.Ширина/1000) * (ЗначенияСвойств.Толщина/1000)), 0);
	Исключение
		Возврат 0;
	КонецПопытки;	
	
КонецФункции

