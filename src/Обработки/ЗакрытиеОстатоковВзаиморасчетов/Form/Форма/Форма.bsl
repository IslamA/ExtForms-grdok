НаСервере
Процедура ЗаполнитьНаСервере()
	
	Запрос = Новый ПостроительЗапроса;
	Запрос.Параметры.Вставить("Период",      Объект.Дата);
	Запрос.Параметры.Вставить("Аналитика",   Объект.Аналитика);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИСТИНА КАК Пометка,
	|	Рег.АналитикаУчетаПоПартнерам,
	|	Рег.ЗаказКлиента,
	|	Рег.Валюта,
	|	Рег.СуммаОстаток КАК Сумма,
	|	Рег.КОплатеОстаток КАК КОплате,
	|	Рег.ОплачиваетсяОстаток КАК Оплачивается,
	|	Рег.КОтгрузкеОстаток КАК КОтгрузке,
	|	Рег.ОтгружаетсяОстаток КАК Отгружается
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами.Остатки(&Период, АналитикаУчетаПоПартнерам = &Аналитика) КАК Рег";
	
	Запрос.Выполнить();
	Объект.РасчетыСКлиентами.Загрузить(Запрос.Результат.Выгрузить());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИСТИНА КАК Пометка,
	|	Рег.АналитикаУчетаПоПартнерам,
	|	Рег.ЗаказКлиента,
	|	Рег.РасчетныйДокумент,
	|	Рег.Валюта,
	|	Рег.УдалитьДатаПлатежа,
	|	Рег.УдалитьКОтгрузкеОстаток КАК УдалитьКОтгрузке,
	|	Рег.УдалитьКОплатеОстаток КАК УдалитьКОплате,
	|	Рег.КВозвратуОстаток КАК КВозврату,
	|	Рег.ДолгОстаток КАК Долг,
	|	Рег.ДолгУпрОстаток КАК ДолгУпр,
	|	Рег.ДолгРеглОстаток КАК ДолгРегл,
	|	Рег.ПредоплатаОстаток КАК Предоплата,
	|	Рег.ПредоплатаУпрОстаток КАК ПредоплатаУпр,
	|	Рег.ПредоплатаРеглОстаток КАК ПредоплатаРегл,
	|	Рег.ЗалогЗаТаруОстаток КАК ЗалогЗаТару,
	|	Рег.ЗалогЗаТаруРеглОстаток КАК ЗалогЗаТаруРегл
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоДокументам.Остатки(&Период, АналитикаУчетаПоПартнерам = &Аналитика) КАК Рег";
	
	Запрос.Выполнить();
	Объект.РасчетыСКлиентамиПоДокументам.Загрузить(Запрос.Результат.Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьДокумент(Команда)
	СформироватьДокументНаСервере();
КонецПроцедуры

&НаСервере
Процедура СформироватьДокументНаСервере()
	
	ДатаДокумента  = Объект.Дата - 1;
		
	ДокументОбъект =  НайтиДокументКорректировку("Сторно остатков взаиморасчетов: " + Строка(Объект.Аналитика), ДатаДокумента);
	ДокументОбъект.ПометкаУдаления = Ложь;
	
	ДокументОбъект.ТаблицаРегистров.Очистить();
	НоваяСтрока = ДокументОбъект.ТаблицаРегистров.Добавить();
	НоваяСтрока.Имя = "РасчетыСКлиентами";
	
	НоваяСтрока = ДокументОбъект.ТаблицаРегистров.Добавить();
	НоваяСтрока.Имя = "РасчетыСКлиентамиПоДокументам";
	
	Если Ложь Тогда
		ДокументОбъект = Документы.КорректировкаРегистров.СоздатьДокумент();
	КонецЕсли;
	
	Для каждого СтрокаТЗ из Объект.РасчетыСКлиентами Цикл
		
		Если НЕ СтрокаТЗ.Пометка Тогда
			Продолжить;
		КонецЕсли;	
		
		Запись = ДокументОбъект.Движения.РасчетыСКлиентами.ДобавитьРасход();
		Запись.Период = ДатаДокумента;
		ЗаполнитьЗначенияСвойств(Запись, СтрокаТЗ);
		
	КонецЦикла;	
	
	Для каждого СтрокаТЗ из Объект.РасчетыСКлиентамиПоДокументам Цикл
		
		Если НЕ СтрокаТЗ.Пометка Тогда
			Продолжить;
		КонецЕсли;	
		
		Запись = ДокументОбъект.Движения.РасчетыСКлиентамиПоДокументам.ДобавитьРасход();
		Запись.Период = ДатаДокумента;
		ЗаполнитьЗначенияСвойств(Запись, СтрокаТЗ);
		
	КонецЦикла;	
	
	ДокументОбъект.Записать();
	ДокументСсылка = ДокументОбъект.Ссылка;
	
КонецПроцедуры

&НаСервере
Функция НайтиДокументКорректировку(Комментарий, Дата)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КорректировкаРегистров.Ссылка
	|ИЗ
	|	Документ.КорректировкаРегистров КАК КорректировкаРегистров
	|ГДЕ
	|	НАЧАЛОПЕРИОДА(КорректировкаРегистров.Дата, ДЕНЬ) = &Дата
	|	И КорректировкаРегистров.Комментарий ПОДОБНО &Комментарий";
	
	Запрос.Параметры.Вставить("Дата",        НачалоДня(Дата));
	Запрос.Параметры.Вставить("Комментарий", Комментарий);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		
		ДокументОбъект = Документы.КорректировкаРегистров.СоздатьДокумент();
		ДокументОбъект.Дата        = КонецДня(Дата);
		ДокументОбъект.Комментарий = Комментарий;
		
	Иначе
		
		ДокументОбъект = Результат.Выгрузить()[0][0].ПолучитьОбъект();
		ДокументОбъект.ПометкаУдаления = Ложь;
		
	КонецЕсли;	
	
	ДокументОбъект.Ответственный = Пользователи.ТекущийПользователь();
	
	Возврат ДокументОбъект;
	
КонецФункции
