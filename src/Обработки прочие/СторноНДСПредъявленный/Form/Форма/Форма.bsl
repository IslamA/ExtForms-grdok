
&НаКлиенте
Процедура Сторно(Команда)
	
	ДокументСсылка = СторноНаСервере();
	ПоказатьОповещениеПользователя("Создан документ", ПолучитьНавигационнуюСсылку(ДокументСсылка), Строка(ДокументСсылка));
	
КонецПроцедуры

&НаСервере
Функция СторноНаСервере()
	
	ДатаОстатков = '2016-07-01';
	ИмяРегистра = Метаданные.РегистрыНакопления.НДСПредъявленный.Имя;
	
	ДокументОбъект =  НайтиДокументКорректировку("Сторно остатков: НДСПредъявленный, " + Организация, ДатаОстатков);
	ДокументОбъект.ПометкаУдаления = Ложь;
	
	ДокументОбъект.ТаблицаРегистров.Очистить();
	НоваяСтрока = ДокументОбъект.ТаблицаРегистров.Добавить();
	НоваяСтрока.Имя = ИмяРегистра;
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("Дата",         '2016-01-01');
	Запрос.Параметры.Вставить("ДатаОстатков", ДатаОстатков);
	Запрос.Параметры.Вставить("Организация",  Организация);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&ДатаОстатков КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	Рег.Организация,
	|	Рег.СчетФактура КАК СчетФактура,
	|	Рег.Поставщик КАК Поставщик,
	|	Рег.ВидЦенности,
	|	Рег.СтавкаНДС,
	|	Рег.ВидДеятельностиНДС,
	|	Рег.ИсправленныйСчетФактура,
	|	Рег.РеализацияЭкспорт КАК РеализацияЭкспорт,
	|	Рег.СуммаБезНДСОстаток КАК СуммаБезНДС,
	|	Рег.НДСОстаток КАК НДС
	|ИЗ
	|	РегистрНакопления.НДСПредъявленный.Остатки(
	|			&ДатаОстатков,
	|			Организация = &Организация
	|				И РеализацияЭкспорт.Дата < &Дата) КАК Рег
	|
	|УПОРЯДОЧИТЬ ПО
	|	Поставщик,
	|	СчетФактура,
	|	РеализацияЭкспорт";
	
	Таблица = Запрос.Выполнить().Выгрузить();
	ДокументОбъект.Движения[ИмяРегистра].Загрузить(Таблица);
	ДокументОбъект.Записать();
	
	Возврат ДокументОбъект.Ссылка;
	
КонецФункции

&НаСервереБезКонтекста
Функция НайтиДокументКорректировку(Комментарий, Дата)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КорректировкаРегистров.Ссылка
	|ИЗ
	|	Документ.КорректировкаРегистров КАК КорректировкаРегистров
	|ГДЕ
	|	НАЧАЛОПЕРИОДА(КорректировкаРегистров.Дата, ДЕНЬ) = &Дата
	|	И КорректировкаРегистров.Комментарий ПОДОБНО &Комментарий";
	
	Запрос.Параметры.Вставить("Дата",        НачалоДня(Дата));
	Запрос.Параметры.Вставить("Комментарий", Комментарий);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		
		ДокументОбъект = Документы.КорректировкаРегистров.СоздатьДокумент();
		ДокументОбъект.Дата        = Дата;
		ДокументОбъект.Комментарий = Комментарий;
		
	Иначе
		
		ДокументОбъект = Результат.Выгрузить()[0][0].ПолучитьОбъект();
		ДокументОбъект.ПометкаУдаления = Ложь;
		
	КонецЕсли;	
	
	ДокументОбъект.Ответственный = Пользователи.ТекущийПользователь();
	
	Возврат ДокументОбъект;
	
КонецФункции