
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Заголовок = Параметры.Заголовок;
	Таймаут = Параметры.Таймаут;
	
	Для Счетчик = 0 По Параметры.Кнопки.Количество() - 1 Цикл
		
		Элемент = Параметры.Кнопки.Получить(Счетчик);
		
		Команда = Команды.Добавить("Команда" + Элемент.Значение);
		Команда.Заголовок = Элемент.Представление;
		Команда.Действие = "ВыполнитьКоманду";
		
		Кнопка = Элементы.Вставить(Команда.Имя, Тип("КнопкаФормы"), Элементы.Группа, );
		Кнопка.ИмяКоманды = Команда.Имя;
		Кнопка.АвтоМаксимальнаяШирина   = Ложь;
		Кнопка.РастягиватьПоГоризонтали = Истина;
		
		Если Счетчик = 0 Тогда
			Кнопка.КнопкаПоУмолчанию = Истина;
			Кнопка.Высота   = 3;
			Кнопка.Пометка  = Истина;
			Кнопка.Шрифт    = Новый Шрифт(,, Истина);
			Кнопка.ЦветФона = WebЦвета.СветлоЗеленый;
		Иначе	
			Кнопка.Высота = 2;
			Кнопка.СочетаниеКлавиш = Новый СочетаниеКлавиш(Клавиша["_" + Счетчик]);
		КонецЕсли;	
		
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Таймаут > 0 Тогда
		ПодключитьОбработчикОжидания("ЗакрытьпоТаймауту", Таймаут, Истина);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьПоТаймауту()
	
	Закрыть();	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьКоманду(Команда)
	
	Закрыть(Сред(Команда.Имя, 8));
	
КонецПроцедуры


