
&НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("Дата",        Период.ДатаОкончания + 1);
	Запрос.Параметры.Вставить("Организация", Организация);
	Запрос.Параметры.Вставить("Сч19",        ПланыСчетов.Хозрасчетный.НайтиПоКоду("19.07"));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Счет КАК Счет,
	|	ХозрасчетныйОстатки.Субконто1 КАК Контрагент,
	|	ХозрасчетныйОстатки.Субконто2 КАК Документ,
	|	ХозрасчетныйОстатки.СуммаОстаток КАК Сумма
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&Дата, Счет = &Сч19, , Организация = &Организация) КАК ХозрасчетныйОстатки
	|ГДЕ
	|	ХозрасчетныйОстатки.СуммаОстаток <= 0";
	
	ТаблицаОстатокв.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Процедура Сторно(Команда)
	СторноНаСервере();
КонецПроцедуры

&НаСервере
Процедура СторноНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("Дата1",       Период.ДатаНачала);
	Запрос.Параметры.Вставить("Дата2",       Период.ДатаОкончания);
	Запрос.Параметры.Вставить("Организация", Организация);
	Запрос.Параметры.Вставить("Таблица",     ТаблицаОстатокв.Выгрузить());
	Запрос.Параметры.Вставить("СобытиеНДС",  Перечисления.СобытияПоНДСПокупки.ПредъявленНДСКВычету0);
	Запрос.Параметры.Вставить("Сч1907",      ПланыСчетов.Хозрасчетный.НайтиПоКоду("19.07"));
	Запрос.Параметры.Вставить("Сч6802",      ПланыСчетов.Хозрасчетный.НайтиПоКоду("68.02"));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таб.Контрагент,
	|	Таб.Документ
	|ПОМЕСТИТЬ Таблица
	|ИЗ
	|	&Таблица КАК Таб
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Рег.Регистратор КАК Регистратор,
	|	Рег.НомерСтроки
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	|			&Дата1,
	|			&Дата2,
	|			Организация = &Организация
	|				И СчетДт = &Сч6802
	|				И СчетКт = &Сч1907
	|				И Регистратор ССЫЛКА Документ.РаспределениеНДС
	|				И (СубконтоКт1, СубконтоКт2) В
	|					(ВЫБРАТЬ
	|						Таб.Контрагент,
	|						Таб.Документ
	|					ИЗ
	|						Таблица КАК Таб),
	|			,
	|			) КАК Рег
	|ИТОГИ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Рег.Регистратор КАК Регистратор,
	|	Рег.НомерСтроки
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок КАК Рег
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Таблица КАК Таблица
	|		ПО Рег.Поставщик = Таблица.Контрагент
	|			И Рег.СчетФактура = Таблица.Документ
	|ГДЕ
	|	Рег.Период МЕЖДУ &Дата1 И &Дата2
	|	И Рег.Организация = &Организация
	|	И Рег.Событие = &СобытиеНДС
	|ИТОГИ ПО
	|	Регистратор";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	// Бух
	Сообщить("РегистрыБухгалтерии.Хозрасчетный");
	Дерево  = МассивРезультатов[1].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	Колво   = Дерево.Строки.Количество();
	Счетчик = 0;
	Для каждого Строка1 Из Дерево.Строки Цикл
		
		Сообщить(Строка1.Регистратор);
		
		НаборЗаписей = РегистрыБухгалтерии.Хозрасчетный.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Строка1.Регистратор);
		НаборЗаписей.Прочитать();
		
		Для каждого Строка2 Из Строка1.Строки Цикл
			
			Запись = НаборЗаписей[Строка2.НомерСтроки - 1];
			Запись.Сумма = 0;
			
		КонецЦикла;	
		
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
	// РегНак
	Сообщить("РегистрыНакопления.НДСЗаписиКнигиПокупок");
	Дерево  = МассивРезультатов[2].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	Колво   = Дерево.Строки.Количество();
	Счетчик = 0;
	Для каждого Строка1 Из Дерево.Строки Цикл
		
		Сообщить(Строка1.Регистратор);
		
		НаборЗаписей = РегистрыНакопления.НДСЗаписиКнигиПокупок.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Строка1.Регистратор);
		НаборЗаписей.Прочитать();
		
		Для каждого Строка2 Из Строка1.Строки Цикл
			
			Запись = НаборЗаписей[Строка2.НомерСтроки - 1];
			Запись.СуммаБезНДС = 0;
			Запись.НДС = 0;
			
		КонецЦикла;	
		
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
КонецПроцедуры
