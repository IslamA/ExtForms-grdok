нецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("АналитикаУчетаПоПартнерам", Объект.КлючАналитики);
	Запрос.Параметры.Вставить("РасчетныйДокумент",         Объект.РасчетныйДокумент);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РасчетыСПоставщикамиПоДокументам.Регистратор,
	|	РасчетыСПоставщикамиПоДокументам.НомерСтроки,
	|	РасчетыСПоставщикамиПоДокументам.Предоплата КАК Сумма
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК РасчетыСПоставщикамиПоДокументам
	|ГДЕ
	|	РасчетыСПоставщикамиПоДокументам.АналитикаУчетаПоПартнерам = &АналитикаУчетаПоПартнерам
	|	И РасчетыСПоставщикамиПоДокументам.РасчетныйДокумент = &РасчетныйДокумент
	|	И РасчетыСПоставщикамиПоДокументам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|
	|УПОРЯДОЧИТЬ ПО
	|	РасчетыСПоставщикамиПоДокументам.Период,
	|	РасчетыСПоставщикамиПоДокументам.Регистратор,
	|	РасчетыСПоставщикамиПоДокументам.НомерСтроки";
	
	Таблица = Запрос.Выполнить().Выгрузить();
	ТаблицаДвижения.Загрузить(Таблица);
	Элементы.ТаблицаДвиженияСумма.ТекстПодвала = Таблица.Итог("Сумма");
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("АналитикаУчетаПоПартнерам", Объект.КлючАналитики);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РасчетыСПоставщикамиПоДокументам.РасчетныйДокумент,
	|	РасчетыСПоставщикамиПоДокументам.ПредоплатаОстаток КАК Сумма
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам.Остатки(, АналитикаУчетаПоПартнерам = &АналитикаУчетаПоПартнерам) КАК РасчетыСПоставщикамиПоДокументам
	|ГДЕ
	|	РасчетыСПоставщикамиПоДокументам.ПредоплатаОстаток > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	РасчетыСПоставщикамиПоДокументам.РасчетныйДокумент.Дата";
	
	Таблица = Запрос.Выполнить().Выгрузить();
	ТаблицаОстатков.Загрузить(Таблица);
	Элементы.ТаблицаОстатковСумма.ТекстПодвала = Таблица.Итог("Сумма");
	
КонецПроцедуры

&НаКлиенте
Процедура Исправить(Команда)
	ИсправитьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ИсправитьНаСервере()
	
	тзТаблицаДвижений = ТаблицаДвижения.Выгрузить();
	тзТаблицаОстатков = ТаблицаОстатков.Выгрузить();
	
	КолвоОстатков = ТаблицаОстатков.Количество();
	
	Счетчик2 = 0;
	Для Счетчик1 = 0 по ТаблицаДвижения.Количество() - 1 Цикл
		
		Строка1 = ТаблицаДвижения[Счетчик1];
		
		НаборЗаписей = РегистрыНакопления.РасчетыСПоставщикамиПоДокументам.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Строка1.Регистратор);
		НаборЗаписей.Прочитать();
		
		Запись = НаборЗаписей[Строка1.НомерСтроки - 1];
		
		Пока Строка1.Сумма > 0 Цикл 
			
			Строка2 = ТаблицаОстатков[Счетчик2];
			Сумма = Мин(Строка1.Сумма, Строка2.Сумма);
			
			Запись.РасчетныйДокумент = Строка2.РасчетныйДокумент;
			Запись.Предоплата     = Сумма;
			Запись.ПредоплатаУпр  = Сумма;
			Запись.ПредоплатаРегл = Сумма;
			
			Строка1.Сумма = Строка1.Сумма - Сумма;
			Строка2.Сумма = Строка2.Сумма - Сумма;
			
			Если Строка1.Сумма > 0 Тогда
				
				НоваяЗапись = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяЗапись, Запись);
				
				Запись = НоваяЗапись;
				
				Запись.РасчетныйДокумент = Объект.РасчетныйДокумент;
				Запись.Предоплата     = Строка1.Сумма;
				Запись.ПредоплатаУпр  = Строка1.Сумма;
				Запись.ПредоплатаРегл = Строка1.Сумма;
				
			КонецЕсли;	
			
			Если Строка2.Сумма = 0 Тогда
				
				Счетчик2 = Счетчик2 + 1;
				Если Счетчик2 = КолвоОстатков Тогда
					Прервать;
				КонецЕсли;	
				
			КонецЕсли;	
			
		КонецЦикла;	
		
		НаборЗаписей.Записать();
		
	КонецЦикла;	
	
КонецПроцедуры
