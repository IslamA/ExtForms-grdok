
&НаКлиенте
Процедура Заполнить(Команда)
	ЗаполнитьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("Свойство", Объект.СтароеСвойство);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИСТИНА КАК Пометка,
	|	СпрДополнительныеРеквизиты.Ссылка КАК Характеристика,
	|	СпрДополнительныеРеквизиты.Значение
	|ИЗ
	|	Справочник.ХарактеристикиНоменклатуры.ДополнительныеРеквизиты КАК СпрДополнительныеРеквизиты
	|ГДЕ
	|	СпрДополнительныеРеквизиты.Свойство = &Свойство";
	
	Таблица = Запрос.Выполнить().Выгрузить();
	ТаблицаДанных.Загрузить(Таблица);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИСТИНА КАК Пометка,
	|	НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Ссылка
	|ИЗ
	|	Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты
	|ГДЕ
	|	НаборыДополнительныхРеквизитовИСведенийДополнительныеРеквизиты.Свойство = &Свойство";
	
	Таблица = Запрос.Выполнить().Выгрузить();
	НаборыРеквизитов.Загрузить(Таблица);
	
КонецПроцедуры

&НаКлиенте
Процедура Заменить(Команда)
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ЗаменитьЗавершение", ЭтаФорма), "Заменить характеристики?", РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет);	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаменитьЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗаменитьНаСервере();
		Состояние("Завершено");
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаменитьНаСервере()
	
	Для каждого СтрокаТЗ из ТаблицаДанных Цикл
		
		Если НЕ СтрокаТЗ.Пометка Тогда
			Продолжить;
		КонецЕсли;
		
		СпрОбъект = СтрокаТЗ.Характеристика.ПолучитьОбъект();
		НайденнаяСтрока = СпрОбъект.ДополнительныеРеквизиты.Найти(Объект.СтароеСвойство, "Свойство");
		Если НайденнаяСтрока <> Неопределено Тогда
			НайденнаяСтрока.Свойство = Объект.НовоеСвойство;
			НайденнаяСтрока.Значение = _ОбщегоНазначенияДоп.вЧисло(СтрокаТЗ.Значение);
			СпрОбъект.ОбменДанными.Загрузка = Истина;
			СпрОбъект.Записать();
		КонецЕсли;	
		
	КонецЦикла;	
	
	Для каждого СтрокаТЗ из НаборыРеквизитов Цикл
		
		Если НЕ СтрокаТЗ.Пометка Тогда
			Продолжить;
		КонецЕсли;
		
		СпрОбъект = СтрокаТЗ.Ссылка.ПолучитьОбъект();
		НайденнаяСтрокаС = СпрОбъект.ДополнительныеРеквизиты.Найти(Объект.СтароеСвойство, "Свойство");
		НайденнаяСтрокаН = СпрОбъект.ДополнительныеРеквизиты.Найти(Объект.НовоеСвойство, "Свойство");
		Если НайденнаяСтрокаС <> Неопределено Тогда
			
			Если НайденнаяСтрокаН = Неопределено Тогда
				НайденнаяСтрокаС.Свойство = Объект.НовоеСвойство;
			Иначе			
			 	СпрОбъект.ДополнительныеРеквизиты.Удалить(НайденнаяСтрокаС);
			КонецЕсли;	
			
			СпрОбъект.ОбменДанными.Загрузка = Истина;
			СпрОбъект.Записать();
			
		КонецЕсли;	
		
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыклВсе(Команда)
	
	Для каждого СтрокаТЗ из ТаблицаДанных Цикл
		СтрокаТЗ.Пометка  = Ложь;
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыклВсеНаборы(Команда)
	
	Для каждого СтрокаТЗ из НаборыРеквизитов Цикл
		СтрокаТЗ.Пометка  = Ложь;
	КонецЦикла;	
	
КонецПроцедуры
