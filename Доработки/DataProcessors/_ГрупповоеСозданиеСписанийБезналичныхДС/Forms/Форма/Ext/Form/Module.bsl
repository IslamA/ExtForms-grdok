
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьПоОснованиям(Параметры.ОбъектыОснований);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоОснованиям(ОбъектыОснований)
	
	Если НЕ ЗначениеЗаполнено(ОбъектыОснований) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Параметры.Вставить("ОбъектыОснований", ОбъектыОснований);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СписаниеДС.Ссылка, СписаниеДСРасшифровкаПлатежа.Ссылка) ЕСТЬ NULL
	|				И Заявка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявокНаРасходованиеДенежныхСредств.КОплате)
	|				И Заявка.Проведен
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Пометка,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(СписаниеДС.Ссылка, СписаниеДСРасшифровкаПлатежа.Ссылка) ЕСТЬ NULL
	|				И Заявка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявокНаРасходованиеДенежныхСредств.КОплате)
	|				И Заявка.Проведен
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Доступность,
	|	Заявка.Ссылка КАК Заявка,
	|	ЕСТЬNULL(СписаниеДС.Ссылка, СписаниеДСРасшифровкаПлатежа.Ссылка)  КАК СписаниеДС,
	|	Заявка.Проведен,
	|	Заявка.Статус
	|ИЗ
	|	Документ.ЗаявкаНаРасходованиеДенежныхСредств КАК Заявка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеБезналичныхДенежныхСредств КАК СписаниеДС
	|		ПО (СписаниеДС.ЗаявкаНаРасходованиеДенежныхСредств = Заявка.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СписаниеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК СписаниеДСРасшифровкаПлатежа
	|		ПО (СписаниеДСРасшифровкаПлатежа.ЗаявкаНаРасходованиеДенежныхСредств = Заявка.Ссылка)
	|ГДЕ
	|	Заявка.Ссылка В(&ОбъектыОснований)";
	
	ТаблицаДанных.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры	

&НаКлиенте
Процедура ВклВсе(Команда)
	
	Для каждого СтрокаТЗ из ТаблицаДанных Цикл
		
		Если НЕ СтрокаТЗ.Доступность Тогда
			Продолжить;
		КонецЕсли;	
		
		СтрокаТЗ.Пометка = Истина;
		
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыклВсе(Команда)
	
	Для каждого СтрокаТЗ из ТаблицаДанных Цикл
		СтрокаТЗ.Пометка = Ложь;
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда)
	
	ПоказатьВопрос(Новый ОписаниеОповещения("СформироватьЗавершение", ЭтаФорма), "Сформирвоать документы?", РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		СформироватьНаСервере();
		ОбновитьОтображениеДанных();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура СформироватьНаСервере()
	
	Для каждого СтрокаТЗ из ТаблицаДанных Цикл
		
		Если НЕ СтрокаТЗ.Пометка Тогда
			Продолжить;
		КонецЕсли;	
		
		ДокументОбъект = Документы.СписаниеБезналичныхДенежныхСредств.СоздатьДокумент();
		ДокументОбъект.Дата = ТекущаяДатаСеанса();
		ДокументОбъект.Заполнить(СтрокаТЗ.Заявка);
		Если НЕ ДокументОбъект.ПроверитьЗаполнение() Тогда
			Продолжить;
		КонецЕсли;	
		
		Попытка
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
			СтрокаТЗ.СписаниеДС = ДокументОбъект.Ссылка;
		Исключение
			Сообщить(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;	
		
	КонецЦикла;	
	
КонецПроцедуры


