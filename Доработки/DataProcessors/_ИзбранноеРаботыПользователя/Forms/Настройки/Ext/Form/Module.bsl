&НаСервере
Процедура СохранитьНастройкиНаСервере()

	СтруктураНастроек = Новый Структура;
	СтруктураНастроек.Вставить("СкрытьНовыеСсылкиИзИзбранного", СкрытьНовыеСсылкиИзИзбранного);
	
	ТаблицаОбщихВнешнихОбъектов = РеквизитФормыВЗначение("СпискиОбщихВнешнихОбъектов");
	
	СписокОбщихВнешнихОбъектов = Новый Массив;
	Для каждого ЭлКоллекции Из СпискиОбщихВнешнихОбъектов Цикл
		СписокОбщихВнешнихОбъектов.Добавить(ЭлКоллекции.ПутьКСписку);
	КонецЦикла;
	СтруктураНастроек.Вставить("СписокОбщихВнешнихОбъектов", СписокОбщихВнешнихОбъектов);
	
	ХранилищеОбщихНастроек.Сохранить("ИзбранноеРаботыПользователя", "Настройки", СтруктураНастроек);

КонецПроцедуры

&НаКлиенте
Процедура СохранитьСпискиОбщихВнешнихОбъектов()
	
	ТД = Новый ТекстовыйДокумент;
	Для каждого СписокВнешнихОбъектов Из СпискиОбщихВнешнихОбъектов Цикл
		Для каждого ЭлКоллекции Из СписокВнешнихОбъектов.СписокВнешнихОбъектов Цикл
			ТД.ДобавитьСтроку(ЭлКоллекции.Значение);
		КонецЦикла; 
		ТД.Записать(СписокВнешнихОбъектов.ПутьКСписку);
	КонецЦикла;

КонецПроцедуры
 
&НаКлиенте
Процедура СохранитьНастройки(Команда)
	
	СохранитьНастройкиНаСервере();
	СохранитьСпискиОбщихВнешнихОбъектов();
	
	Закрыть(Истина);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНастройкиНаСервере()

	СтруктураНастроек = ХранилищеОбщихНастроек.Загрузить("ИзбранноеРаботыПользователя", "Настройки");
	Если СтруктураНастроек = Неопределено Тогда
		Возврат;
	КонецЕсли; 

	СкрытьНовыеСсылкиИзИзбранного = СтруктураНастроек.СкрытьНовыеСсылкиИзИзбранного;
	
	СпискиОбщихВнешнихОбъектов.Очистить();
	
	СписокОбщихВнешнихОбъектов = СтруктураНастроек.СписокОбщихВнешнихОбъектов;
	Для каждого ПолноеИмяФайла Из СписокОбщихВнешнихОбъектов Цикл
		НовыйСписок = СпискиОбщихВнешнихОбъектов.Добавить();
		НовыйСписок.ПутьКСписку = ПолноеИмяФайла;
	КонецЦикла; 
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ФильтрВнешнихОбъектов = Параметры.ФильтрВнешнихОбъектов;
	
	ЗагрузитьНастройкиНаСервере();
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьСписокВнешнихОбъектов(ПутьКСписку)
	
	СписокВнешнихОбъектов = Новый Массив;
	
	ТД = Новый ТекстовыйДокумент;
	ТД.Прочитать(ПутьКСписку);
	Для Сч = 1 По ТД.КоличествоСтрок() Цикл
		СписокВнешнихОбъектов.Добавить(ТД.ПолучитьСтроку(Сч));
	КонецЦикла; 

	Возврат СписокВнешнихОбъектов;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьПодборСпискаОбщихОбъектов()

	ДВ = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДВ.Фильтр = "Список общих объектов (*.txt)|*.txt";
	ДВ.МножественныйВыбор = Истина;
	
	Если НЕ ДВ.Выбрать() Тогда
		Возврат
	КонецЕсли; 
	
	Для каждого ПолноеИмяФайла Из ДВ.ВыбранныеФайлы Цикл
		Если СпискиОбщихВнешнихОбъектов.НайтиСтроки(Новый Структура("ПутьКСписку", ПолноеИмяФайла)).Количество() = 0 Тогда
			НовыйСписок = СпискиОбщихВнешнихОбъектов.Добавить();
			НовыйСписок.ПутьКСписку = ПолноеИмяФайла;
			НовыйСписок.СписокВнешнихОбъектов.ЗагрузитьЗначения(ПолучитьСписокВнешнихОбъектов(ПолноеИмяФайла));
		КонецЕсли; 
	КонецЦикла; 
	
	СпискиОбщихВнешнихОбъектовМодифицированы = Истина;
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПодборВнешнихОбъектов()
	
	ТекущиеДанные = Элементы.СпискиОбщихВнешнихОбъектов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДВ = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДВ.Фильтр = ФильтрВнешнихОбъектов;
	ДВ.МножественныйВыбор = Истина;
	
	Если НЕ ДВ.Выбрать() Тогда
		Возврат
	КонецЕсли; 
	
	СписокВнешнихОбъектов = ТекущиеДанные.СписокВнешнихОбъектов;
	
	Для каждого ПолноеИмяФайла Из ДВ.ВыбранныеФайлы Цикл
		Если СписокВнешнихОбъектов.НайтиПоЗначению(ПолноеИмяФайла) = Неопределено Тогда
			НовыйСписок = СписокВнешнихОбъектов.Добавить(ПолноеИмяФайла);
		КонецЕсли; 
	КонецЦикла; 
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СпискиОбщихВнешнихОбъектовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	ОткрытьПодборСпискаОбщихОбъектов();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВнешнихОбъектовПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВнешнихОбъектовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	ОткрытьПодборВнешнихОбъектов();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовыйСписок(Команда)
	
	ДВ = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ДВ.Фильтр = "Список общих объектов (*.txt)|*.txt";
	
	Если НЕ ДВ.Выбрать() Тогда
		Возврат
	КонецЕсли; 
	
	Если СпискиОбщихВнешнихОбъектов.НайтиСтроки(Новый Структура("ПутьКСписку", ДВ.ПолноеИмяФайла)).Количество() <> 0 Тогда
		Предупреждение(НСтр("ru = 'Список уже добавлен'") ); 
		Возврат;
	КонецЕсли; 
	
	НовыйСписок = СпискиОбщихВнешнихОбъектов.Добавить();
	НовыйСписок.ПутьКСписку = ДВ.ПолноеИмяФайла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Для каждого ЭлКоллекции Из СпискиОбщихВнешнихОбъектов Цикл
		ЭлКоллекции.СписокВнешнихОбъектов.ЗагрузитьЗначения(ПолучитьСписокВнешнихОбъектов(ЭлКоллекции.ПутьКСписку));
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура СпискиОбщихВнешнихОбъектовПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

