
Процедура ПриСозданииНаСервере_ФормаДокумента(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	Объект = Форма.Объект;
	
	Элемент = Форма.Элементы.Вставить("Сотрудники_РольИсполнителяРабот", Тип("ПолеФормы"), Форма.Элементы.Сотрудники, Форма.Элементы.СотрудникиКТУ);
   	Элемент.Вид         = ВидПоляФормы.ПолеВвода;
	Элемент.ПутьКДанным = "Объект.Сотрудники._РольИсполнителяРабот";
	
	Элемент = Форма.Элементы.СотрудникиКТУ;
	Элемент.Видимость = Ложь;
	
	Команда = Форма.Команды.Добавить("_ЗаполнитьСостав");
	Команда.Заголовок = "Заполнить состав";
	Команда.ИзменяетСохраняемыеДанные = Истина;
	Команда.Действие  = "_ЗаполнитьСостав";
	
	Кнопка = Форма.Элементы.Вставить("_ЗаполнитьСостав", Тип("КнопкаФормы"), Форма.Элементы.Сотрудники.КоманднаяПанель, );
	Кнопка.ИмяКоманды = "_ЗаполнитьСостав";
	
КонецПроцедуры

Процедура ПриСозданииНаСервере_ФормаСписка(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	Список = Форма.Документы; 
	Список.ПроизвольныйЗапрос = Истина;
	ТекстЗапроса = "ВЫБРАТЬ * ИЗ Документ.ФормированиеСоставаБригады КАК ФормированиеСоставаБригады";
	
	СхемаЗапроса = Новый СхемаЗапроса;
    СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);	
	
	ЗапросВыбораСхемыЗапроса = СхемаЗапроса.ПакетЗапросов[0];
	Оператор = ЗапросВыбораСхемыЗапроса.Операторы[0];
	
	ТекстПоля =
	"ВЫБОР
	|	КОГДА ИСТИНА В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				Документ.ТабельУчетаРабочегоВремени КАК Док
	|			ГДЕ
	|				Док._Основание = ФормированиеСоставаБригады.Ссылка)
	|		ТОГДА ИСТИНА
	|	ИНАЧЕ ЛОЖЬ
	|КОНЕЦ"; 
	_ОбработчикиПроведенияСервер.ДобавитьПолеВЗапрос(ЗапросВыбораСхемыЗапроса, Оператор, "_ЕстьТабель", ТекстПоля);
 
	ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
	Список.ТекстЗапроса = ТекстЗапроса;
	
	Элемент = Форма.Элементы.Вставить("_ЕстьТабель", Тип("ПолеФормы"), Форма.Элементы.Документы, );
    Элемент.Вид         = ВидПоляФормы.ПолеФлажка;
	Элемент.ПутьКДанным = "Документы._ЕстьТабель";
	Элемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	
КонецПроцедуры

Процедура ЗаполнитьСостав(Объект) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ФормированиеСоставаБригады.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ФормированиеСоставаБригады
	|ИЗ
	|	(ВЫБРАТЬ
	|		МАКСИМУМ(ФормированиеСоставаБригады.НачалоПериода) КАК Дата
	|	ИЗ
	|		Документ.ФормированиеСоставаБригады КАК ФормированиеСоставаБригады
	|	ГДЕ
	|		ФормированиеСоставаБригады.НачалоПериода <= &Дата
	|		И ФормированиеСоставаБригады.Бригада = &Бригада
	|		И ФормированиеСоставаБригады.Проведен) КАК Выборка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ФормированиеСоставаБригады КАК ФормированиеСоставаБригады
	|		ПО Выборка.Дата = ФормированиеСоставаБригады.НачалоПериода
	|			И (ФормированиеСоставаБригады.Бригада = &Бригада)
	|			И (ФормированиеСоставаБригады.Проведен)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка УБЫВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокСотрудники.Сотрудник КАК Сотрудник,
	|	ДокСотрудники.НормативныйКТУ КАК НормативныйКТУ,
	|	ДокСотрудники._РольИсполнителяРабот КАК _РольИсполнителяРабот
	|ИЗ
	|	Документ.ФормированиеСоставаБригады.Сотрудники КАК ДокСотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ФормированиеСоставаБригады КАК ФормированиеСоставаБригады
	|		ПО (ФормированиеСоставаБригады.Ссылка = ДокСотрудники.Ссылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокСотрудники.НомерСтроки";
	
	Запрос.УстановитьПараметр("Бригада", Объект.Бригада);
	Запрос.УстановитьПараметр("Дата",    КонецДня(?(ЗначениеЗаполнено(Объект.Дата), Объект.Дата, ТекущаяДата())));
	
	Таблица = Запрос.Выполнить().Выгрузить();
	Объект.Сотрудники.Очистить();
	Для каждого СтрокаТЗ из Таблица Цикл
		
		Если _ОбщегоНазначенияДоп.ФизлицоУволено(СтрокаТЗ.Сотрудник) Тогда
			Сообщить("" + СтрокаТЗ.Сотрудник + " уволен");
			Продолжить;
		КонецЕсли;	
		
		НоваяСтрока = Объект.Сотрудники.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ); 
		
	КонецЦикла;	
	
КонецПроцедуры	