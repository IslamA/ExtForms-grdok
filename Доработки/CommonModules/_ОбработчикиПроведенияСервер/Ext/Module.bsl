
Процедура ДополнитьТекстыЗапросов(Запрос, ТекстыЗапроса, Таблицы, ДобавитьРазделитель, ДобавлятьСловоТаблица, ТолькоОтмеченные) Экспорт
    
    // Дорабатываем тексты запросов перед выполнением
    // в функции  ПроведениеСервер.ИницализироватьТаблицыДляДвижений
    Ссылка = Неопределено;
    Если НЕ Запрос.Параметры.Свойство("Ссылка", Ссылка) Тогда
        Возврат;
    КонецЕсли;    
    
    ТипСсылки = ТипЗнч(Ссылка) ;
    Если ТипСсылки = Тип("ДокументСсылка.ЗаявкаНаРасходованиеДенежныхСредств") Тогда    
        _ЗаявкаНаРасходованиеДС.ТекстЗапросаПроведение(Запрос, ТекстыЗапроса, "");
		
    ИначеЕсли ТипСсылки = Тип("ДокументСсылка.ПоступлениеУслугПрочихАктивов") Тогда    
        _ПоступлениеУслугПрочихАктивов.ТекстЗапросаПроведение(Запрос, ТекстыЗапроса, "");
    КонецЕсли;    
    
КонецПроцедуры

#Область Общие

Функция НайтиЭлементСписка(ТекстыЗапроса, ИмяРегистра) Экспорт
	
	НайденныйЭлементСписка = Неопределено;
	Для каждого ЭлементаСписка из ТекстыЗапроса Цикл
		
		Если ЭлементаСписка.Представление = ИмяРегистра Тогда
			НайденныйЭлементСписка = ЭлементаСписка;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;	
	
	Возврат НайденныйЭлементСписка;
	
КонецФункции	

Процедура УстановитьПолеЗапроса(ЗапросВыбора, Оператор, ИмяПоля, Выражение) Экспорт
	
	Колонки = ЗапросВыбора.Колонки;
	Колонка = Колонки.Найти(ИмяПоля);
	Индекс  = Колонки.Индекс(Колонка);
	
	Оператор.ВыбираемыеПоля.Установить(Индекс, Новый ВыражениеСхемыЗапроса(Выражение));
	
КонецПроцедуры	

Процедура ДобавитьДоступноеПолеВТаблицу(Оператор, ПсевдонимТаблицы, Поля) Экспорт
	
	Если ТипЗнч(Поля) = Тип("Массив") Тогда
		МассивПолей = Поля;
	Иначе
		МассивПолей = Новый Массив;
		МассивПолей.Добавить(Поля);
	КонецЕсли;	
	
	Для каждого Источник из Оператор.Источники Цикл
		
		Если Источник.Источник.Псевдоним = ПсевдонимТаблицы Тогда
			
			Для каждого ИмяПоля из МассивПолей Цикл
				Источник.Источник.ДоступныеПоля.Добавить(ИмяПоля);
			КонецЦикла;	
				
		КонецЕсли;	
		
	КонецЦикла;	
	
КонецПроцедуры

Процедура ДобавитьПолеВЗапрос(ЗапросВыбора, Оператор, Псевдоним, Выражение) Экспорт
	
	Колонки = ЗапросВыбора.Колонки;
	
	Оператор.ВыбираемыеПоля.Добавить(Выражение);
	Колонки[Колонки.Количество() - 1].Псевдоним = Псевдоним;
	
КонецПроцедуры

Процедура ДобавитьУсловиеСоединения(Оператор, Таблица1, Таблица2, Условие) Экспорт
	
	Для каждого Источник из Оператор.Источники Цикл
		
		Если Источник.Источник.Псевдоним = Таблица1 Тогда
			
			Соединение = Источник.Соединения.Добавить(Таблица2, Условие);
			Возврат;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры	

#КонецОбласти
